!DECK ACOS
REAL FUNCTION ACOS(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  ACOS
  !***PURPOSE  Compute the arccosine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      SINGLE PRECISION (ACOS-S, DACOS-D)
  !***KEYWORDS  ACOS, ARCCOSINE, ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL ACOS, X, Y
  !      Y = ACOS(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 ACOS.
  !
  ! *Function Return Values:
  !
  !    ACOS    :    the arccosine of X.
  !
  ! *Description:
  !
  !    ACOS evaluates the arccosine of an argument.  The argument X
  !    must be in the interval -1.0 .LE. X .LE. +1.0.  For arguments
  !    outside the permitted interval, an error message is generated
  !    by XERMSG and the routine aborts.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  ASIN, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900516  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  ACOS
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL pi2
  !     .. External Functions ..
  REAL ASIN
  EXTERNAL ASIN
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC ABS
  !     .. Data statements ..
  DATA pi2/1.57079632679489661923E0/
  !***FIRST EXECUTABLE STATEMENT  ACOS
  IF ( ABS(X)>1.0E0 ) CALL XERMSG('SLATEC','ACOS','ABS(X) GREATER THAN 1',1,&
    2)
  !
  ACOS = pi2 - ASIN(X)
  !
END FUNCTION ACOS
!DECK ALOG
REAL FUNCTION ALOG(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  ALOG
  !***PURPOSE  Compute the natural logarithm of a number.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4B
  !***TYPE      SINGLE PRECISION (ALOG-S, DLOG-D, CLOG-C)
  !***KEYWORDS  ALOG, ELEMENTARY FUNCTION, FORTRAN INTRINSIC,
  !             NATURAL LOGARITHM
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL ALOG, X, Y
  !      Y = ALOG(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 ALOG.
  !
  ! *Function Return Values:
  !
  !    ALOG    :    the natural logarithm of X.
  !
  ! *Description:
  !
  !    ALOG evaluates the natural logarithm of an argument.  The
  !    argument X must be positive.  For a non-positive argument, an
  !    error message is generated by XERMSG and the routine aborts.
  !
  !
  !    SERIES FOR ALN     ON THE INTERVAL  0.          TO  3.46021D-03
  !                                        WITH WEIGHTED ERROR   1.50E-16
  !                                         LOG WEIGHTED ERROR  15.82
  !                               SIGNIFICANT FIGURES REQUIRED  15.65
  !                                    DECIMAL PLACES REQUIRED  16.21
  !
  !    ALN2 = ALOG(2.0) - 0.625
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  CSEVL, INITS, R1MACH, R9UPAK, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900517  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  ALOG
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL aln2, t, t2, xn, y
  INTEGER n, nterms, ntrval
  !     .. Local Arrays ..
  REAL alncen(5), alncs(6), center(4)
  !     .. External Functions ..
  REAL CSEVL, R1MACH
  INTEGER INITS
  EXTERNAL CSEVL, R1MACH, INITS
  !     .. External Subroutines ..
  EXTERNAL R9UPAK, XERMSG
  !     .. Save statement ..
  SAVE nterms
  !     .. Data statements ..
  DATA alncs(1)/1.3347199877973882E0/
  DATA alncs(2)/.000693756283284112E0/
  DATA alncs(3)/.000000429340390204E0/
  DATA alncs(4)/.000000000289338477E0/
  DATA alncs(5)/.000000000000205125E0/
  DATA alncs(6)/.000000000000000150E0/
  DATA center(1)/1.0/
  DATA center(2)/1.25/
  DATA center(3)/1.50/
  DATA center(4)/1.75/
  DATA alncen(1)/0.0E0/
  DATA alncen(2)/ + .223143551314209755E+0/
  DATA alncen(3)/ + .405465108108164381E+0/
  DATA alncen(4)/ + .559615787935422686E+0/
  DATA alncen(5)/ + .693147180559945309E+0/
  DATA aln2/0.068147180559945309E0/
  DATA nterms/0/
  !***FIRST EXECUTABLE STATEMENT  ALOG
  IF ( nterms==0 ) nterms = INITS(alncs,6,28.9*R1MACH(3))
  !
  IF ( X<=0.0E0 ) CALL XERMSG('SLATEC','ALOG','X IS ZERO OR NEGATIVE',1,2)
  !
  CALL R9UPAK(X,y,n)
  !
  xn = n - 1
  y = 2.0E0*y
  ntrval = INT( 4.0E0*y - 2.5E0 )
  IF ( ntrval==5 ) t = ((y-1.0E0)-1.0E0)/(y+2.0E0)
  IF ( ntrval<5 ) t = (y-center(ntrval))/(y+center(ntrval))
  t2 = t*t
  !
  ALOG = 0.625E0*xn + &
    (aln2*xn+alncen(ntrval)+2.0E0*t+t*t2*CSEVL(578.0E0*t2-1.0E0,alncs,&
    nterms))
  !
END FUNCTION ALOG
!DECK ALOG10
REAL FUNCTION ALOG10(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  ALOG10
  !***PURPOSE  Compute the common base 10 logarithm.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4B
  !***TYPE      SINGLE PRECISION (ALOG10-S, DLOG10-D)
  !***KEYWORDS  ALOG10, BASE 10 LOGARITHM, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL ALOG10, X, Y
  !      Y = ALOG10(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 ALOG.
  !
  !
  ! *Function Return Values:
  !
  !    ALOG10  :    the  base 10, i.e. common, logarithm.
  !
  ! *Description:
  !
  !    ALOG10 evaluates the base 10 logarithm of an argument.  The
  !    argument X must be positive.  For a non-positive argument, an
  !    error message is generated by XERMSG and the routine aborts.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  ALOG
  !***REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900517  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  ALOG10
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL aloge
  !     .. External Functions ..
  REAL ALOG
  EXTERNAL ALOG
  !     .. Data statements ..
  DATA aloge/0.43429448190325182765E0/
  !***FIRST EXECUTABLE STATEMENT  ALOG10
  ALOG10 = aloge*ALOG(X)
  !
END FUNCTION ALOG10
!DECK ASIN
REAL FUNCTION ASIN(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  ASIN
  !***PURPOSE  Compute the arcsine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      SINGLE PRECISION (ASIN-S, DASIN-D)
  !***KEYWORDS  ARCSINE, ASIN, ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL ASIN, X, Y
  !      Y = ASIN(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 ASIN.
  !
  ! *Function Return Values:
  !
  !    ASIN    :    the arcsine of X.
  !
  ! *Description:
  !
  !    ASIN evaluates the arcsine of an argument.  The absolute
  !    value of the argument X must be less than or equal to 1.0.
  !    For arguments outside the permitted interval, an error message
  !    is generated by XERMSG and the routine aborts.
  !
  !    SERIES FOR ASIN    ON THE INTERVAL  0.          TO  5.00000D-01
  !                                        WITH WEIGHTED ERROR   1.60E-17
  !                                         LOG WEIGHTED ERROR  16.79
  !                               SIGNIFICANT FIGURES REQUIRED  15.67
  !                                    DECIMAL PLACES REQUIRED  17.45
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  CSEVL, INITS, R1MACH, SQRT, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900518  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  ASIN
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL pi2, sqeps, y, z
  INTEGER nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL asincs(20)
  !     .. External Functions ..
  REAL CSEVL, R1MACH, SQRT
  INTEGER INITS
  EXTERNAL CSEVL, R1MACH, SQRT, INITS
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC ABS, SIGN
  !     .. Save statement ..
  SAVE first, nterms, sqeps
  !     .. Data statements ..
  DATA asincs(1)/.10246391753227159E0/
  DATA asincs(2)/.054946487221245833E0/
  DATA asincs(3)/.004080630392544969E0/
  DATA asincs(4)/.000407890068546044E0/
  DATA asincs(5)/.000046985367432203E0/
  DATA asincs(6)/.000005880975813970E0/
  DATA asincs(7)/.000000777323124627E0/
  DATA asincs(8)/.000000106774233400E0/
  DATA asincs(9)/.000000015092399536E0/
  DATA asincs(10)/.000000002180972408E0/
  DATA asincs(11)/.000000000320759842E0/
  DATA asincs(12)/.000000000047855369E0/
  DATA asincs(13)/.000000000007225128E0/
  DATA asincs(14)/.000000000001101833E0/
  DATA asincs(15)/.000000000000169476E0/
  DATA asincs(16)/.000000000000026261E0/
  DATA asincs(17)/.000000000000004095E0/
  DATA asincs(18)/.000000000000000642E0/
  DATA asincs(19)/.000000000000000101E0/
  DATA asincs(20)/.000000000000000016E0/
  DATA pi2/1.57079632679489661923E0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  ASIN
  IF ( first ) THEN
    nterms = INITS(asincs,20,0.1*R1MACH(3))
    sqeps = SQRT(6.0E0*R1MACH(3))
  ENDIF
  first = .FALSE.
  !
  y = ABS(X)
  IF ( y>1.0E0 ) CALL XERMSG('SLATEC','ASIN','ABS(X) GREATER THAN 1',1,2)
  !
  z = 0.0E0
  IF ( y>sqeps ) z = y*y
  IF ( z<=0.5E0 ) THEN
    ASIN = X*(1.0E0+CSEVL(4.0E0*z-1.0E0,asincs,nterms))
  ELSE
    ASIN = pi2 - SQRT(1.0E0-z)*(1.0E0+CSEVL(3.0E0-4.0E0*z,asincs,nterms))
  ENDIF
  IF ( X/=0.0E0 ) ASIN = SIGN(ASIN,X)
  !
END FUNCTION ASIN
!DECK ATAN
REAL FUNCTION ATAN(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  ATAN
  !***PURPOSE  Compute the arctangent.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      SINGLE PRECISION (ATAN-S, DATAN-D)
  !***KEYWORDS  ARCTANGENT, ATAN, ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL ATAN, X, Y
  !      Y = ATAN(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 ATAN.
  !
  ! *Function Return Values:
  !
  !    ATAN    :    the arctangent.
  !
  ! *Description:
  !
  !    ATAN evaluates the arctangent of an argument.
  !
  !    SERIES FOR ATAN    ON THE INTERVAL  0.          TO  4.00000D-02
  !                                        WITH WEIGHTED ERROR   1.00E-17
  !                                         LOG WEIGHTED ERROR  17.00
  !                               SIGNIFICANT FIGURES REQUIRED  16.38
  !                                    DECIMAL PLACES REQUIRED  17.48
  !
  !    XBNDN = TAN((2*N-1)*PI/16.0)
  !
  !    TANP8(N) = TAN(N*PI/8.)
  !
  !    CONPI8(N) + PI8(N) = N*PI/8.0
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  CSEVL, INITS, R1MACH, SQRT
  !***REVISION HISTORY  (YYMMDD)
  !   780101  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900518  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  ATAN
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL sqeps, t, xbig, xbnd1, xbnd2, xbnd3, xbnd4, y
  INTEGER n, nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL atancs(9), conpi8(4), pi8(4), tanp8(3)
  !     .. External Functions ..
  REAL CSEVL, R1MACH, SQRT
  INTEGER INITS
  EXTERNAL CSEVL, R1MACH, SQRT, INITS
  !     .. Intrinsic Functions ..
  INTRINSIC ABS, SIGN
  !     .. Save statement ..
  SAVE first, nterms, sqeps, xbig
  !     .. Data statements ..
  DATA atancs(1)/.48690110349241406E0/
  DATA atancs(2)/ - .006510831636717464E0/
  DATA atancs(3)/.000038345828265245E0/
  DATA atancs(4)/ - .000000268722128762E0/
  DATA atancs(5)/.000000002050093098E0/
  DATA atancs(6)/ - .000000000016450717E0/
  DATA atancs(7)/.000000000000136509E0/
  DATA atancs(8)/ - .000000000000001160E0/
  DATA atancs(9)/.000000000000000010E0/
  DATA xbnd1/.198912367379658006E+0/
  DATA xbnd2/.668178637919298919E+0/
  DATA xbnd3/1.49660576266548901E+0/
  DATA xbnd4/5.02733949212584810E+0/
  DATA tanp8(1)/.414213562373095048E+0/
  DATA tanp8(2)/1.0E0/
  DATA tanp8(3)/2.41421356237309504E+0/
  DATA conpi8(1)/0.375E0/
  DATA conpi8(2)/0.75E0/
  DATA conpi8(3)/1.125E0/
  DATA conpi8(4)/1.5E0/
  DATA pi8(1)/0.176990816987241548E-1/
  DATA pi8(2)/0.353981633974483096E-1/
  DATA pi8(3)/0.530972450961724644E-1/
  DATA pi8(4)/0.0707963267948966192E0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  ATAN
  IF ( first ) THEN
    nterms = INITS(atancs,9,0.1*R1MACH(3))
    sqeps = SQRT(6.0E0*R1MACH(3))
    xbig = 1.0E0/R1MACH(3)
  ENDIF
  first = .FALSE.
  !
  y = ABS(X)
  IF ( y<=xbnd1 ) THEN
    !
    ATAN = X
    IF ( y>sqeps ) ATAN = X*(0.75E0+CSEVL(50.0E0*y**2-1.0E0,atancs,nterms))
    !
  ELSEIF ( y<=xbnd4 ) THEN
    !
    n = 1
    IF ( y>xbnd2 ) n = 2
    IF ( y>xbnd3 ) n = 3
    !
    t = (y-tanp8(n))/(1.0E0+y*tanp8(n))
    ATAN = SIGN(conpi8(n)&
      +(pi8(n)+t*(0.75E0+CSEVL(50.0E0*t**2-1.0E0,atancs,nterms))),X)
  ELSE
    !
    ATAN = conpi8(4) + pi8(4)
    IF ( y<xbig ) ATAN = conpi8(4)&
      + (pi8(4)-(0.75E0+CSEVL(50.0E0/y**2-1.0E0,atancs,&
      nterms))/y)
    ATAN = SIGN(ATAN,X)
  ENDIF
  !
END FUNCTION ATAN
!DECK ATAN2
REAL FUNCTION ATAN2(Sn,Cs)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  ATAN2
  !***PURPOSE  Compute the arctangent in the proper quadrant.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      SINGLE PRECISION (ATAN2-S, DATAN2-D)
  !***KEYWORDS  ARCTANGENT, ATAN, ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL ATAN2, SN, CS, Y
  !      Y = ATAN2(SN, CS)
  !
  ! *Arguments:
  !
  !    SN      :IN  This is an argument.  It will not be modified by
  !                 ATAN2.
  !    CS      :IN  This is an argument.  It will not be modified by
  !                 ATAN2.
  !
  ! *Function Return Values:
  !
  !    ATAN2   :    the arctangent of SN and CS in the proper quadrant.
  !
  ! *Description:
  !
  !    ATAN2 evaluates the arctangent of two arguments in the proper
  !    quadrant.  The arguments SN and CS must not both be zero.  If
  !    both of the arguments are zero, an error message is generated
  !    by XERMSG and the routine aborts.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  ATAN, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900518  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  ATAN2
  !     .. Scalar Arguments ..
  REAL Cs, Sn
  !     .. Local Scalars ..
  REAL pi
  !     .. External Functions ..
  REAL ATAN
  EXTERNAL ATAN
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC SIGN
  !     .. Data statements ..
  DATA pi/3.14159265358979323846E0/
  !***FIRST EXECUTABLE STATEMENT  ATAN2
  IF ( Cs/=0.0E0 ) THEN
    ATAN2 = ATAN(Sn/Cs)
    IF ( Cs<0.0E0 ) ATAN2 = ATAN2 + pi
    IF ( ATAN2>pi ) ATAN2 = ATAN2 - 2.0*pi
  ELSE
    IF ( Sn==0.0E0 ) CALL XERMSG('SLATEC','ATAN2','BOTH ARGUMENTS ARE ZERO',&
      1,2)
    ATAN2 = SIGN(0.5E0*pi,Sn)
  ENDIF
  !
END FUNCTION ATAN2
!DECK CABS
REAL FUNCTION CABS(Z)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  CABS
  !***PURPOSE  Compute the absolute value of a complex number.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4
  !***TYPE      COMPLEX (CABS-C)
  !***KEYWORDS  CABS, COMPLEX ABSOLUTE VALUE, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL A, CABS
  !      COMPLEX Z
  !      A = CABS(Z)
  !
  ! *Arguments:
  !
  !    Z       :IN  This is the argument.  It will not be modified by
  !                 CABS.
  !
  ! *Function Return Values:
  !
  !    CABS    :    the absolute value of a complex argument.
  !
  ! *Description:
  !
  !    CABS evaluates the absolute value of a complex argument.  If the
  !    real part of the argument Z is X and the imaginary part is Y, then
  !    CABS(Z) mathematically is SQRT(X**2+Y**2).
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  R1MACH, SQRT
  !***REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900518  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  CABS
  !     .. Scalar Arguments ..
  COMPLEX Z
  !     .. Local Scalars ..
  REAL r1, r2, sqeps, x, y
  LOGICAL first
  !     .. External Functions ..
  REAL R1MACH, SQRT
  EXTERNAL R1MACH, SQRT
  !     .. Intrinsic Functions ..
  INTRINSIC ABS, AIMAG, MAX, MIN, REAL
  !     .. Save statement ..
  SAVE first, sqeps
  !     .. Data statements ..
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  CABS
  IF ( first ) sqeps = SQRT(R1MACH(4))
  first = .FALSE.
  !
  x = ABS(REAL(Z))
  y = ABS(AIMAG(Z))
  r1 = MIN(x,y)
  r2 = MAX(x,y)
  !
  CABS = r2
  IF ( r1>r2*sqeps ) CABS = r2*SQRT(1.0E0+(r1/r2)**2)
  !
END FUNCTION CABS
!DECK CCOS
COMPLEX FUNCTION CCOS(Z)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  CCOS
  !***PURPOSE  Compute the cosine of a complex argument.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      COMPLEX (COS-S, DCOS-D, CCOS-C)
  !***KEYWORDS  CCOS, COMPLEX COSINE, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      COMPLEX CCOS, Y, Z
  !      Y = CCOS(Z)
  !
  ! *Arguments:
  !
  !    Z       :IN  This is the argument.  It will not be modified by
  !                 CCOS.
  !
  ! *Function Return Values:
  !
  !    CCOS    :    the cosine of a complex argument.
  !
  ! *Description:
  !
  !    CCOS evaluates the cosine of a complex argument.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  COS, COSH, SIN, SINH, XERCLR
  !***REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900518  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  CCOS
  !     .. Scalar Arguments ..
  COMPLEX Z
  !     .. Local Scalars ..
  REAL cs, x, y
  !     .. External Functions ..
  REAL COS, COSH, SIN, SINH
  EXTERNAL COS, COSH, SIN, SINH
  !     .. External Subroutines ..
  EXTERNAL XERCLR
  !     .. Intrinsic Functions ..
  INTRINSIC AIMAG, CMPLX, REAL
  !***FIRST EXECUTABLE STATEMENT  CCOS
  x = REAL(Z)
  y = AIMAG(Z)
  !
  cs = COS(x)
  CALL XERCLR
  !
  CCOS = CMPLX(cs*COSH(y),-SIN(x)*SINH(y))
  !
END FUNCTION CCOS
!DECK CEXP
COMPLEX FUNCTION CEXP(Z)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  CEXP
  !***PURPOSE  Compute the complex exponential.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4B
  !***TYPE      COMPLEX (EXP-S, DEXP-D, CEXP-C)
  !***KEYWORDS  CEXP, COMPLEX EXPONENTIAL, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      COMPLEX CEXP, Y, Z
  !      Y = CEXP(Z)
  !
  ! *Arguments:
  !
  !    Z       :IN  This is the argument.  It will not be modified by
  !                 CEXP.
  !
  ! *Function Return Values:
  !
  !    CEXP    :    the exponential of a complex argument.
  !
  ! *Description:
  !
  !    CEXP evaluates the exponential, i.e., exp(Z) or e**Z.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  COS, EXP, SIN
  !***REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900518  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  CEXP
  !     .. Scalar Arguments ..
  COMPLEX Z
  !     .. Local Scalars ..
  REAL r, y
  !     .. External Functions ..
  REAL COS, EXP, SIN
  EXTERNAL COS, EXP, SIN
  !     .. Intrinsic Functions ..
  INTRINSIC AIMAG, CMPLX, REAL
  !***FIRST EXECUTABLE STATEMENT  CEXP
  r = EXP(REAL(Z))
  IF ( r==0.0E0 ) THEN
    CEXP = (0.0E0,0.0E0)
  ELSE
    y = AIMAG(Z)
    CEXP = CMPLX(r*COS(y),r*SIN(y))
  ENDIF
  !
END FUNCTION CEXP
!DECK CLOG
COMPLEX FUNCTION CLOG(Z)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  CLOG
  !***PURPOSE  Compute the complex natural logarithm.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4B
  !***TYPE      COMPLEX (ALOG-S, DLOG-D, CLOG-C)
  !***KEYWORDS  CLOG, COMPLEX NATURAL LOGARITHM, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      COMPLEX CLOG, Y, Z
  !      Y = CLOG(Z)
  !
  ! *Arguments:
  !
  !    Z       :IN  This is the argument.  It will not be modified by
  !                 CLOG.
  !
  ! *Function Return Values:
  !
  !    CLOG    :    the natural logarithm of a complex argument.
  !
  ! *Description:
  !
  !    CLOG evaluates the natural logarithm of a complex argument.
  !
  !***REFERENCES  CARG
  !***ROUTINES CALLED  ALOG, CABS, CARG
  !***REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900518  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  CLOG
  !     .. Scalar Arguments ..
  COMPLEX Z
  !     .. External Functions ..
  REAL ALOG, CABS, CARG
  EXTERNAL ALOG, CABS, CARG
  !     .. Intrinsic Functions ..
  INTRINSIC CMPLX
  !***FIRST EXECUTABLE STATEMENT  CLOG
  CLOG = CMPLX(ALOG(CABS(Z)),CARG(Z))
  !
END FUNCTION CLOG
!DECK COS
REAL FUNCTION COS(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  COS
  !***PURPOSE  Compute the cosine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      SINGLE PRECISION (COS-S, DCOS-D, CCOS-C)
  !***KEYWORDS  COS, COSINE, ELEMENTARY FUNCTION, FORTRAN INTRNSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL COS, X, Y
  !      Y = COS(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 COS.
  !
  ! *Function Return Values:
  !
  !    COS     :    the cosine of X.
  !
  ! *Description:
  !
  !    COS evaluates the cosine of an argument.  If the argument X is
  !    greater than the reciprocal of R1MACH(4), an error message is
  !    generated by XERMSG and the routine aborts.
  !
  !    SERIES FOR SIN     ON THE INTERVAL  0.          TO  1.00000D+00
  !                                        WITH WEIGHTED ERROR   2.47E-18
  !                                         LOG WEIGHTED ERROR  17.61
  !                               SIGNIFICANT FIGURES REQUIRED  16.20
  !                                    DECIMAL PLACES REQUIRED  18.06
  !
  !
  !    SERIES FOR COS     ON THE INTERVAL  0.          TO  1.00000D+00
  !                                        WITH WEIGHTED ERROR   4.71E-17
  !                                         LOG WEIGHTED ERROR  16.33
  !                               SIGNIFICANT FIGURES REQUIRED  15.58
  !                                    DECIMAL PLACES REQUIRED  16.78
  !
  !    PI4REC = 4.0/PI - 1.0
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  CSEVL, INITS, R1MACH, SQRT, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900518  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  COS
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL ainty, ainty2, eta, pi4rec, xmax, xsml, xwarn, y, yrem, z
  INTEGER ifn, noctnt, ntcs, ntsn
  LOGICAL first
  !     .. Local Arrays ..
  REAL coscs(8), sincs(8)
  !     .. External Functions ..
  REAL CSEVL, R1MACH, SQRT
  INTEGER INITS
  EXTERNAL CSEVL, R1MACH, SQRT, INITS
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC ABS, AINT, AMOD, MOD
  !     .. Save statement ..
  SAVE first, ntcs, ntsn, xmax, xsml, xwarn
  !     .. Data statements ..
  DATA sincs(1)/ - .008104079085448715E0/
  DATA sincs(2)/ - .039144567527081957E0/
  DATA sincs(3)/.000304509420678944E0/
  DATA sincs(4)/ - .000001123574976796E0/
  DATA sincs(5)/.000000002414039972E0/
  DATA sincs(6)/ - .000000000003391636E0/
  DATA sincs(7)/.000000000000003358E0/
  DATA sincs(8)/ - .000000000000000002E0/
  DATA coscs(1)/.20326382740961603E0/
  DATA coscs(2)/ - .14643664439083686E0/
  DATA coscs(3)/.001921449311814646E0/
  DATA coscs(4)/ - .000009964968489829E0/
  DATA coscs(5)/.000000027576595607E0/
  DATA coscs(6)/ - .000000000047399498E0/
  DATA coscs(7)/.000000000000055495E0/
  DATA coscs(8)/ - .000000000000000047E0/
  DATA pi4rec/ + .273239544735162686E0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  COS
  IF ( first ) THEN
    eta = 0.1E0*R1MACH(3)
    ntsn = INITS(sincs,8,eta)
    ntcs = INITS(coscs,8,eta)
    xsml = SQRT(2.0E0*R1MACH(3))
    xmax = 1.0E0/R1MACH(4)
    xwarn = SQRT(xmax)
  ENDIF
  first = .FALSE.
  !
  y = ABS(X)
  IF ( y>xmax ) CALL XERMSG('SLATEC','COS',&
    'NO PRECISION BECAUSE ABS(X) IS TOO BIG',2,2)
  IF ( y>xwarn ) CALL XERMSG('SLATEC','COS',&
    'ANSWER LT HALF PRECISION BECAUSE ABS(X) IS BIG'&
    ,1,1)
  !
  IF ( y<xsml ) THEN
    COS = 1.0E0
  ELSE
    !
    !       CAREFULLY COMPUTE Y * (4/PI)
    !         = (AINT(Y) + REM(Y)) * (1.0 + PI4REC)
    !         = AINT(Y) + REM(Y) + Y*PI4REC
    !         = AINT(Y) + AINT(REM(Y)+Y*PI4REC) + REM(REM(Y)+Y*PI4REC)
    !
    ainty = AINT(y)
    yrem = y - ainty
    y = yrem + y*pi4rec
    ainty2 = AINT(y)
    ainty = ainty + ainty2
    y = y - ainty2
    !
    noctnt = INT( AMOD(ainty,8.0E0) )
    ifn = MOD((noctnt+1)/2,2)
    IF ( MOD(noctnt,2)==1 ) y = 1.0E0 - y
    z = 2.0E0*y**2 - 1.0E0
    !
    IF ( ifn/=1 ) THEN
      COS = 0.75E0 + CSEVL(z,coscs,ntcs)
    ELSE
      COS = y*(0.75E0+CSEVL(z,sincs,ntsn))
    ENDIF
    IF ( noctnt>1.AND.noctnt<6 ) COS = -COS
  ENDIF
  !
END FUNCTION COS
!DECK COSH
REAL FUNCTION COSH(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  COSH
  !***PURPOSE  Compute the hyperbolic cosine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4C
  !***TYPE      SINGLE PRECISION (COSH-S, DCOSH-D)
  !***KEYWORDS  COSH, ELEMENTARY FUNCTION, FORTRAN INTRINSIC,
  !             HYPERBOLIC COSINE
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS NATIONAL LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      COMPLEX COSH, X, Y
  !      Y = COSH(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 COSH.
  !
  ! *Function Return Values:
  !
  !    COSH    :    the hyperbolic cosine of X.
  !
  ! *Description:
  !
  !    COSH evaluates the hyperbolic cosine of an argument.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  EXP, R1MACH, SQRT
  !***REVISION HISTORY  (YYMMDD)
  !   770810  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900518  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  COSH
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL y, ymax
  !     .. External Functions ..
  REAL EXP, R1MACH, SQRT
  EXTERNAL EXP, R1MACH, SQRT
  !     .. Intrinsic Functions ..
  INTRINSIC ABS
  !     .. Data statements ..
  DATA ymax/0.0/
  !***FIRST EXECUTABLE STATEMENT  COSH
  IF ( ymax==0.0E0 ) ymax = 1.0E0/SQRT(R1MACH(3))
  !
  y = EXP(ABS(X))
  COSH = 0.5E0*y
  IF ( y<ymax ) COSH = 0.5E0*(y+1.0E0/y)
  !
END FUNCTION COSH
!DECK CSIN
COMPLEX FUNCTION CSIN(Z)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  CSIN
  !***PURPOSE  Compute the complex sine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      COMPLEX (SIN-S, DSIN-D, CSIN-C)
  !***KEYWORDS  COMPLEX SINE, CSIN, ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  Fullerton, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      COMPLEX CSIN, Y, Z
  !      Y = CSIN(Z)
  !
  ! *Arguments:
  !
  !    Z       :IN  This is the argument.  It will not be modified by
  !                 CSIN.
  !
  ! *Function Return Values:
  !
  !    CSIN    :    the sine of a complex argument.
  !
  ! *Description:
  !
  !    CSIN evaluates the sine of a complex argument.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  COS, COSH, SIN, SINH, XERCLR
  !***REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900518  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  CSIN
  !     .. Scalar Arguments ..
  COMPLEX Z
  !     .. Local Scalars ..
  REAL sn, x, y
  !     .. External Functions ..
  REAL COS, COSH, SIN, SINH
  EXTERNAL COS, COSH, SIN, SINH
  !     .. External Subroutines ..
  EXTERNAL XERCLR
  !     .. Intrinsic Functions ..
  INTRINSIC AIMAG, CMPLX, REAL
  !***FIRST EXECUTABLE STATEMENT  CSIN
  x = REAL(Z)
  y = AIMAG(Z)
  !
  sn = SIN(x)
  CALL XERCLR
  !
  CSIN = CMPLX(sn*COSH(y),COS(x)*SINH(y))
  !
END FUNCTION CSIN
!DECK CSQRT
COMPLEX FUNCTION CSQRT(Z)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  CSQRT
  !***PURPOSE  Compute the complex square root.
  !***LIBRARY   FNLIB
  !***CATEGORY  C2
  !***TYPE      COMPLEX (SQRT-S, DSQRT-D, CSQRT-C)
  !***KEYWORDS  COMPLEX SQUARE ROOT, CSQRT, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      COMPLEX CSQRT, Y, Z
  !      Y = CSQRT(Z)
  !
  ! *Arguments:
  !
  !    Z       :IN  This is the argument.  It will not be modified by
  !                 CSQRT.
  !
  ! *Function Return Values:
  !
  !    CSQRT   :    the square root of a complex argument.
  !
  ! *Description:
  !
  !    CSQRT evaluates the square root of a complex argument.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  CABS, SQRT
  !***REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   801001  Revisions by W. Fullerton
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  CSQRT
  !     .. Scalar Arguments ..
  COMPLEX Z
  !     .. Local Scalars ..
  REAL r, x, xtmp, y, ytmp
  !     .. External Functions ..
  REAL CABS, SQRT
  EXTERNAL CABS, SQRT
  !     .. Intrinsic Functions ..
  INTRINSIC ABS, AIMAG, CMPLX, REAL, SIGN
  !***FIRST EXECUTABLE STATEMENT  CSQRT
  x = REAL(Z)
  y = AIMAG(Z)
  r = CABS(Z)
  !
  IF ( r==0.0E0 ) THEN
    CSQRT = (0.0E0,0.0E0)
  ELSE
    !
    xtmp = SQRT((r+ABS(x))*0.5E0)
    ytmp = y*0.5E0/xtmp
    !
    IF ( x>=0.0E0 ) THEN
      CSQRT = CMPLX(xtmp,ytmp)
    ELSEIF ( y==0.0E0 ) THEN
      CSQRT = CMPLX( 0., SQRT(r) )
    ELSE !IF ( x<0.0E0 ) THEN
      CSQRT = CMPLX(ABS(ytmp),SIGN(xtmp,y))
    ENDIF
  ENDIF
  !
END FUNCTION CSQRT
!DECK DACOS
REAL(8) FUNCTION DACOS(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DACOS
  !***PURPOSE  Compute the double precision arccosine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      DOUBLE PRECISION (ACOS-S, DACOS-D)
  !***KEYWORDS  DACOS, DOUBLE PRECISION ARCCOSINE, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DACOS, X, Y
  !      Y = DACOS(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DACOS.
  !
  ! *Function Return Values:
  !
  !    DACOS   :    the double precision arccosine of X.
  !
  ! *Description:
  !
  !    DACOS evaluates the double precision arccosine of a double
  !    precision argument.  The argument X must be in the interval -1.0
  !    .LE. X .LE. +1.0.  For arguments outside the permitted interval,
  !    an error message is generated by XERMSG and the routine aborts.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  DASIN, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DACOS
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: pi2
  !     .. External Functions ..
  REAL(8) :: DASIN
  EXTERNAL DASIN
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC DABS
  !     .. Data statements ..
  DATA pi2/1.57079632679489661923132169163975D0/
  !***FIRST EXECUTABLE STATEMENT  DACOS
  IF ( DABS(X)>1.0D0 ) CALL XERMSG('SLATEC','DACOS','DABS(X) GREATER THAN ',&
    1,2)
  !
  DACOS = pi2 - DASIN(X)
  !
END FUNCTION DACOS
!DECK DASIN
REAL(8) FUNCTION DASIN(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DASIN
  !***PURPOSE  Compute the double precision arcsine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      DOUBLE PRECISION (ASIN-S, DASIN-D)
  !***KEYWORDS  DASIN, DOUBLE PRECISION ARCSINE, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DASIN, X, Y
  !      Y = DASIN(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DASIN.
  !
  ! *Function Return Values:
  !
  !    DASIN   :    the double precision arcsine of X.
  !
  ! *Description:
  !
  !    DASIN evaluates the double precision arcsine of a double precision
  !    argument.  The value for the argument X must be in the interval
  !    -1.0 .LE. X .LE. +1.0.  For arguments outside the permitted
  !    interval, an error message is generated by XERMSG and the
  !    routine aborts.
  !
  !    SERIES FOR ASIN    ON THE INTERVAL  0.          TO  5.00000E-01
  !                                        WITH WEIGHTED ERROR   1.62E-32
  !                                         LOG WEIGHTED ERROR  31.79
  !                               SIGNIFICANT FIGURES REQUIRED  30.67
  !                                    DECIMAL PLACES REQUIRED  32.59
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, DCSEVL, DSQRT, INITDS, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DASIN
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: pi2, sqeps, y, z
  INTEGER nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL(8) :: asincs(39)
  !     .. External Functions ..
  REAL(8) :: D1MACH, DCSEVL, DSQRT
  INTEGER INITDS
  EXTERNAL D1MACH, DCSEVL, DSQRT, INITDS
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC DABS, DSIGN, SNGL
  !     .. Save statement ..
  SAVE first, nterms, sqeps
  !     .. Data statements ..
  DATA asincs(1)/ + .10246391753227159336573148305785D+0/
  DATA asincs(2)/ + .54946487221245833306011195902924D-1/
  DATA asincs(3)/ + .40806303925449692851307056149246D-2/
  DATA asincs(4)/ + .40789006854604435455598823905612D-3/
  DATA asincs(5)/ + .46985367432203691616048530136218D-4/
  DATA asincs(6)/ + .58809758139708058986454385552074D-5/
  DATA asincs(7)/ + .77732312462777632750557528163795D-6/
  DATA asincs(8)/ + .10677423340082039235047504956587D-6/
  DATA asincs(9)/ + .15092399536022808262386434401064D-7/
  DATA asincs(10)/ + .21809724080055385496609614713930D-8/
  DATA asincs(11)/ + .32075984262789614433261959667376D-9/
  DATA asincs(12)/ + .47855369646781034461493133918953D-10/
  DATA asincs(13)/ + .72251287362910432263848754537112D-11/
  DATA asincs(14)/ + .11018334742255783705372701334987D-11/
  DATA asincs(15)/ + .16947632539203354877423745651078D-12/
  DATA asincs(16)/ + .26261558667348224162283241502416D-13/
  DATA asincs(17)/ + .40958299813281178408828069291110D-14/
  DATA asincs(18)/ + .64244793108803655891727944887091D-15/
  DATA asincs(19)/ + .10128142198228221693973361222041D-15/
  DATA asincs(20)/ + .16039221897380787560050597464746D-16/
  DATA asincs(21)/ + .25503501355807141715298789676373D-17/
  DATA asincs(22)/ + .40701403797862382855487165672106D-18/
  DATA asincs(23)/ + .65172671712881144437889267575466D-19/
  DATA asincs(24)/ + .10467453037096796954244891716266D-19/
  DATA asincs(25)/ + .16858725563380328094989095185066D-20/
  DATA asincs(26)/ + .27221936305040227625164341247999D-21/
  DATA asincs(27)/ + .44059293900347550617126830079999D-22/
  DATA asincs(28)/ + .71466685243375937853063168000000D-23/
  DATA asincs(29)/ + .11615793343859516051798971733333D-23/
  DATA asincs(30)/ + .18915234552354685801184187733333D-24/
  DATA asincs(31)/ + .30855772044244342399827968000000D-25/
  DATA asincs(32)/ + .50416366022162453412970495999999D-26/
  DATA asincs(33)/ + .82502725502400865081753600000000D-27/
  DATA asincs(34)/ + .13520032631020947208055466666666D-27/
  DATA asincs(35)/ + .22184326876541720216644266666666D-28/
  DATA asincs(36)/ + .36442494054085079212578133333333D-29/
  DATA asincs(37)/ + .59920218558643813307733333333333D-30/
  DATA asincs(38)/ + .98584812059573785810261333333333D-31/
  DATA asincs(39)/ + .16222501166399014393173333333333D-31/
  DATA pi2/1.57079632679489661923132169163975D0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  DASIN
  IF ( first ) THEN
    nterms = INITDS(asincs,39,0.1*SNGL(D1MACH(3)))
    sqeps = DSQRT(6.0D0*D1MACH(3))
  ENDIF
  first = .FALSE.
  y = DABS(X)
  IF ( y>1.0D0 ) CALL XERMSG('SLATEC','DASIN','DABS(X) GREATER THAN 1',1,2)
  !
  z = 0.0D0
  IF ( y>sqeps ) z = y*y
  IF ( z<=0.5D0 ) THEN
    DASIN = X*(1.0D0+DCSEVL(4.0D0*z-1.0D0,asincs,nterms))
  ELSE
    DASIN = pi2 - DSQRT(1.0D0-z)*(1.0D0+DCSEVL(3.0D0-4.0D0*z,asincs,nterms))
  ENDIF
  IF ( X/=0.0D0 ) DASIN = DSIGN(DASIN,X)
  !
END FUNCTION DASIN
!DECK DATAN
REAL(8) FUNCTION DATAN(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DATAN
  !***PURPOSE  Compute the double precision arctangent.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      DOUBLE PRECISION (ATAN-S, DATAN-D)
  !***KEYWORDS  DATAN, DOUBLE PRECISION ARCTANGENT, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DATAN, X, Y
  !      Y = DATAN(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DATAN.
  !
  ! *Function Return Values:
  !
  !    DATAN   :    the double precision arctangent of X.
  !
  ! *Description:
  !
  !    DATAN evaluates the double precision arctangent of a double
  !    precision argument.
  !
  !    SERIES FOR ATAN    ON THE INTERVAL  0.          TO  4.00000E-02
  !                                        WITH WEIGHTED ERROR   4.83E-32
  !                                         LOG WEIGHTED ERROR  31.32
  !                               SIGNIFICANT FIGURES REQUIRED  30.70
  !                                    DECIMAL PLACES REQUIRED  31.92
  !
  !    XBNDN = TAN((2*N-1)*PI/16.0)
  !
  !    TANP8(N) = TAN(N*PI/8.0)
  !
  !    CONPI8(N) + PI8(N) = N*PI/8.0
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, DCSEVL, DSQRT, INITDS
  !***REVISION HISTORY  (YYMMDD)
  !   780101  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.
  !***END PROLOGUE  DATAN
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: sqeps, t, xbig, xbnd1, xbnd2, xbnd3, xbnd4, y
  INTEGER n, nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL(8) :: atancs(16), conpi8(4), pi8(4), tanp8(3)
  !     .. External Functions ..
  REAL(8) :: D1MACH, DCSEVL, DSQRT
  INTEGER INITDS
  EXTERNAL D1MACH, DCSEVL, DSQRT, INITDS
  !     .. Intrinsic Functions ..
  INTRINSIC DABS, DSIGN, SNGL
  !     .. Save statement ..
  SAVE first, nterms, sqeps, xbig
  !     .. Data statements ..
  DATA atancs(1)/ + .48690110349241406474636915902891D+0/
  DATA atancs(2)/ - .65108316367174641818869794945974D-2/
  DATA atancs(3)/ + .38345828265245177653569992430456D-4/
  DATA atancs(4)/ - .26872212876223146539595410518788D-6/
  DATA atancs(5)/ + .20500930985824269846636514686688D-8/
  DATA atancs(6)/ - .16450717395484269455734135285348D-10/
  DATA atancs(7)/ + .13650975274390773423813528484428D-12/
  DATA atancs(8)/ - .11601779591998246322891309834666D-14/
  DATA atancs(9)/ + .10038333943866273835797657402666D-16/
  DATA atancs(10)/ - .88072747152163859327073696000000D-19/
  DATA atancs(11)/ + .78136321005661722180580266666666D-21/
  DATA atancs(12)/ - .69954535148267456086613333333333D-23/
  DATA atancs(13)/ + .63105905713702136004266666666666D-25/
  DATA atancs(14)/ - .57296075370213874346666666666666D-27/
  DATA atancs(15)/ + .52274796280602282666666666666666D-29/
  DATA atancs(16)/ - .48327903911608320000000000000000D-31/
  DATA xbnd1/ + .19891236737965800691159762264467D+0/
  DATA xbnd2/ + .66817863791929891999775768652308D+0/
  DATA xbnd3/ + 1.4966057626654890176011351349424D+0/
  DATA xbnd4/ + 5.0273394921258481045149750710640D+0/
  DATA tanp8(1)/ + .41421356237309504880168872420969D+0/
  DATA tanp8(2)/ + 1.0D0/
  DATA tanp8(3)/ + 2.4142135623730950488016887242096D+0/
  DATA conpi8(1)/0.375D0/
  DATA conpi8(2)/0.75D0/
  DATA conpi8(3)/1.125D0/
  DATA conpi8(4)/1.5D0/
  DATA pi8(1)/ + .17699081698724154807830422909937D-1/
  DATA pi8(2)/ + .35398163397448309615660845819875D-1/
  DATA pi8(3)/ + .53097245096172464423491268729813D-1/
  DATA pi8(4)/ + .70796326794896619231321691639751D-1/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  DATAN
  IF ( first ) THEN
    nterms = INITDS(atancs,16,0.1*SNGL(D1MACH(3)))
    sqeps = DSQRT(6.0D0*D1MACH(3))
    xbig = 1.0D0/D1MACH(3)
  ENDIF
  first = .FALSE.
  !
  y = DABS(X)
  IF ( y<=xbnd1 ) THEN
    !
    DATAN = X
    IF ( y>sqeps ) DATAN = X*(0.75D0+DCSEVL(50.0D0*y*y-1.0D0,atancs,nterms))
    !
  ELSEIF ( y<=xbnd4 ) THEN
    !
    n = 1
    IF ( y>xbnd2 ) n = 2
    IF ( y>xbnd3 ) n = 3
    !
    t = (y-tanp8(n))/(1.0D0+y*tanp8(n))
    DATAN = DSIGN(conpi8(n)&
      +(pi8(n)+t*(0.75D0+DCSEVL(50.0D0*t*t-1.0D0,atancs,nterms))),X)
  ELSE
    !
    DATAN = conpi8(4) + pi8(4)
    IF ( y<xbig ) DATAN = conpi8(4)&
      + (pi8(4)-(0.75D0+DCSEVL(50.0D0/y**2-1.0D0,atancs,&
      nterms))/y)
    DATAN = DSIGN(DATAN,X)
  ENDIF
  !
END FUNCTION DATAN
!DECK DATAN2
REAL(8) FUNCTION DATAN2(Sn,Cs)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DATAN2
  !***PURPOSE  Compute the double precision arctangent in the
  !            proper quadrant.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      DOUBLE PRECISION (ATAN2-S, DATAN2-D)
  !***KEYWORDS  DATAN2, DOUBLE PRECISION ARCTANGENT, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DATAN2, SN, CS, Y
  !      Y = DATAN2(SN, CS)
  !
  ! *Arguments:
  !
  !    SN      :IN  This is an argument.  It will not be modified by
  !                 DATAN2.
  !    CS      :IN  This is an argument.  It will not be modified by
  !                 DATAN2.
  !
  ! *Function Return Values:
  !
  !    DATAN2  :    the double precision arctangent in the proper
  !                 quadrant.
  !
  ! *Description:
  !
  !    DATAN2 evaluates the double precision arctangent of two double
  !    precison arguments in the proper quadrant.  The arguments SN and CS
  !    must not both be zero.  If both of the two arguments are zero,
  !    an error message is generated by XERMSG and the routine aborts.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  DATAN, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DATAN2
  !     .. Scalar Arguments ..
  REAL(8) :: Cs, Sn
  !     .. Local Scalars ..
  REAL(8) :: pi
  !     .. External Functions ..
  REAL(8) :: DATAN
  EXTERNAL DATAN
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC DSIGN
  !     .. Data statements ..
  DATA pi/3.14159265358979323846264338327950D0/
  !***FIRST EXECUTABLE STATEMENT  DATAN2
  IF ( Cs/=0.0D0 ) THEN
    DATAN2 = DATAN(Sn/Cs)
    IF ( Cs<0.0D0 ) DATAN2 = DATAN2 + pi
    IF ( DATAN2>pi ) DATAN2 = DATAN2 - 2.0D0*pi
  ELSE
    IF ( Sn==0.0D0 ) CALL XERMSG('SLATEC','DATAN2','BOTH ARGUMENTS ARE ZERO'&
      ,1,2)
    !
    DATAN2 = DSIGN(0.5D0*pi,Sn)
  ENDIF
  !
END FUNCTION DATAN2
!DECK DCOS
REAL(8) FUNCTION DCOS(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DCOS
  !***PURPOSE  Compute the double precision cosine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      DOUBLE PRECISION (COS-S, DCOS-D, CCOS-C)
  !***KEYWORDS  DCOS, DOUBLE PRECISION COSINE, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DCOS, X, Y
  !      Y = DCOS(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DCOS.
  !
  ! *Function Return Values:
  !
  !    DCOS    :    the double precision cosine of X.
  !
  ! *Description:
  !
  !    DCOS evaluates the double precision cosine of a double precision
  !    argument.  If the argument X is greater than the reciprocal
  !    of D1MACH(4), an error message is generated by XERMSG and
  !    the routine aborts.
  !
  !
  !    SERIES FOR SIN     ON THE INTERVAL  0.          TO  1.00000E+00
  !                                        WITH WEIGHTED ERROR   7.08E-32
  !                                         LOG WEIGHTED ERROR  31.15
  !                               SIGNIFICANT FIGURES REQUIRED  29.74
  !                                    DECIMAL PLACES REQUIRED  31.69
  !
  !    SERIES FOR COS     ON THE INTERVAL  0.          TO  1.00000E+00
  !                                        WITH WEIGHTED ERROR   5.80E-34
  !                                         LOG WEIGHTED ERROR  33.24
  !                               SIGNIFICANT FIGURES REQUIRED  32.49
  !                                    DECIMAL PLACES REQUIRED  33.79
  !
  !    PI4REC = 4.0/PI - 1.0
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, DCSEVL, DINT, DSQRT, INITDS, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DCOS
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: ainty, ainty2, pi4rec, xmax, xsml, xwarn, y, &
    yrem, z
  REAL eta
  INTEGER ifn, noctnt, ntcs, ntsn
  LOGICAL first
  !     .. Local Arrays ..
  REAL(8) :: coscs(13), sincs(12)
  !     .. External Functions ..
  REAL(8) :: D1MACH, DCSEVL, DINT, DSQRT
  INTEGER INITDS
  EXTERNAL D1MACH, DCSEVL, DINT, DSQRT, INITDS
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC DABS, DMOD, MOD
  !     .. Save statement ..
  SAVE first, ntcs, ntsn, xsml, xmax, xwarn
  !     .. Data statements ..
  DATA sincs(1)/ - .8104079085448715800196469943559D-2/
  DATA sincs(2)/ - .3914456752708195701742853890074D-1/
  DATA sincs(3)/ + .3045094206789444055815695590878D-3/
  DATA sincs(4)/ - .1123574976796415958214203624984D-5/
  DATA sincs(5)/ + .2414039972413749607105791078174D-8/
  DATA sincs(6)/ - .3391636705037535474000613712810D-11/
  DATA sincs(7)/ + .3358087618514203445901588366650D-14/
  DATA sincs(8)/ - .2468983320993182257103205610666D-17/
  DATA sincs(9)/ + .1401147833510729936175146666666D-20/
  DATA sincs(10)/ - .6322858930224709680426666666666D-24/
  DATA sincs(11)/ + .2323065248071953216000000000000D-27/
  DATA sincs(12)/ - .7083757638662399999999999999999D-31/
  DATA coscs(1)/ + .203263827409616025400812030121852D+0/
  DATA coscs(2)/ - .146436644390836863320796360139993D+0/
  DATA coscs(3)/ + .192144931181464679690714543745079D-2/
  DATA coscs(4)/ - .996496848982930006866910618423658D-5/
  DATA coscs(5)/ + .275765956071873951864383935301797D-7/
  DATA coscs(6)/ - .473994980816484403744229510321398D-10/
  DATA coscs(7)/ + .554954854148518274082726416274480D-13/
  DATA coscs(8)/ - .470970490651755595660385183093333D-16/
  DATA coscs(9)/ + .302989760807937313388574986666666D-19/
  DATA coscs(10)/ - .152841493421461533614912000000000D-22/
  DATA coscs(11)/ + .620745154357827253973333333333333D-26/
  DATA coscs(12)/ - .207333072298361599999999999999999D-29/
  DATA coscs(13)/ + .579538533367466666666666666666666D-33/
  DATA pi4rec/ + .27323954473516268615107010698011D0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  DCOS
  IF ( first ) THEN
    eta = REAL( 0.1D0*D1MACH(3), 4 )
    ntsn = INITDS(sincs,12,eta)
    ntcs = INITDS(coscs,13,eta)
    xsml = DSQRT(2.0D0*D1MACH(3))
    xmax = 1.0D0/D1MACH(4)
    xwarn = DSQRT(xmax)
  ENDIF
  first = .FALSE.
  !
  y = DABS(X)
  IF ( y>xmax ) CALL XERMSG('SLATEC','DCOS',&
    'NO PRECISION BECAUSE DABS(X) IS TOO BIG',2,2)
  IF ( y>xwarn ) CALL XERMSG('SLATEC','DCOS',&
    'ANSWER LT HALF PRECISION BECAUSE DABS(X) IS BIG'&
    ,1,1)
  !
  IF ( y<xsml ) THEN
    DCOS = 1.0D0
  ELSE
    !
    !       CAREFULLY COMPUTE Y * (4/PI)
    !         = (AINT(Y) + REM(Y)) * (1. + PI4REC)
    !         = AINT(Y) + REM(Y) + Y*PI4REC
    !         = AINT(Y) + AINT(REM(Y)+Y*PI4REC) + REM(REM(Y)+Y*PI4REC)
    !
    ainty = DINT(y)
    yrem = y - ainty
    y = yrem + y*pi4rec
    ainty2 = DINT(y)
    ainty = ainty + ainty2
    y = y - ainty2
    !
    noctnt = INT( DMOD(ainty,8.0D0) )
    ifn = MOD((noctnt+1)/2,2)
    IF ( MOD(noctnt,2)==1 ) y = 1.0D0 - y
    z = 2.0D0*y*y - 1.0D0
    !
    IF ( ifn/=1 ) THEN
      DCOS = 0.75D0 + DCSEVL(z,coscs,ntcs)
    ELSE
      DCOS = y*(0.75D0+DCSEVL(z,sincs,ntsn))
    ENDIF
    IF ( noctnt>1.AND.noctnt<6 ) DCOS = -DCOS
  ENDIF
  !
END FUNCTION DCOS
!DECK DCOSH
REAL(8) FUNCTION DCOSH(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DCOSH
  !***PURPOSE  Compute the double precision hyperbolic cosine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4C
  !***TYPE      DOUBLE PRECISION (COSH-S, DCOSH-D)
  !***KEYWORDS  DCOSH, DOUBLE PRECISION HYPERBOLIC COSINE,
  !             ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DCOSH, X, Y
  !      Y = DCOSH(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DCOSH.
  !
  ! *Function Return Values:
  !
  !    DCOSH   :    the double precision hyperbolic cosine of X.
  !
  ! *Description:
  !
  !    DCOSH evaluates the double precision hyperbolic cosine of a
  !    double precision argument.  If the magnitude of the argument
  !    X is too big (i.e., greater than ln(D1MACH(2) - 0.01),
  !    an error message is generated by XERMSG and the routine aborts.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, DEXP, DSQRT
  !***REVISION HISTORY  (YYMMDD)
  !   770801  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DCOSH
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: y, ymax
  !     .. External Functions ..
  REAL(8) :: D1MACH, DEXP, DSQRT
  EXTERNAL D1MACH, DEXP, DSQRT
  !     .. Intrinsic Functions ..
  INTRINSIC DABS
  !     .. Save statement ..
  SAVE ymax
  !     .. Data statements ..
  DATA ymax/0.0D0/
  !***FIRST EXECUTABLE STATEMENT  DCOSH
  IF ( ymax==0.0D0 ) ymax = 1.0D0/DSQRT(D1MACH(3))
  !
  y = DEXP(DABS(X))
  DCOSH = 0.5D0*y
  IF ( y<ymax ) DCOSH = 0.5D0*(y+1.0D0/y)
  !
END FUNCTION DCOSH
!DECK DEXP
REAL(8) FUNCTION DEXP(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DEXP
  !***PURPOSE  Compute the double precision exponential.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4B
  !***TYPE      DOUBLE PRECISION (DEXP-D, CEXP-C)
  !***KEYWORDS  DEXP, DOUBLE PRECISION EXPONENTIAL LOGARITHM,
  !             ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DEXP, X, Y
  !      Y = DEXP(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DEXP.
  !
  ! *Function Return Values:
  !
  !    DEXP    :    the double precision exponential of X.
  !
  ! *Description:
  !
  !    DEXP evaluates the double precision exponential of a double
  !    precision argument, i.e. exp(Z) or e**Z.  If the argument X is
  !    too big, (ln(D1MACH(2)) - 0.01), an error message is generated by
  !    XERMSG and the routine aborts.
  !
  !    SERIES FOR EXP     ON THE INTERVAL -1.00000E+00 TO  1.00000E+00
  !                                        WITH WEIGHTED ERROR   2.30E-34
  !                                         LOG WEIGHTED ERROR  33.64
  !                               SIGNIFICANT FIGURES REQUIRED  32.28
  !                                    DECIMAL PLACES REQUIRED  34.21
  !
  !    TWON16(I) IS 2.0**((I-1)/16) - 1.0
  !
  !    ALN216 IS 16.0/ALOG(2.) - 23.0
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, D9PAK, DCSEVL, DINT, DLOG, INITDS, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900525  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DEXP
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: aln216, f, xint, xmax, xmin, y
  INTEGER n, n16, ndx, nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL(8) :: expcs(14), twon16(17)
  !     .. External Functions ..
  REAL(8) :: D1MACH, D9PAK, DCSEVL, DINT, DLOG
  INTEGER INITDS
  EXTERNAL D1MACH, D9PAK, DCSEVL, DINT, DLOG, INITDS
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC SNGL
  !     .. Save statement ..
  SAVE first, nterms, xmin, xmax
  !     .. Data statements ..
  DATA expcs(1)/ + .866569493314985712733404647266231D-1/
  DATA expcs(2)/ + .938494869299839561896336579701203D-3/
  DATA expcs(3)/ + .677603970998168264074353014653601D-5/
  DATA expcs(4)/ + .366931200393805927801891250687610D-7/
  DATA expcs(5)/ + .158959053617461844641928517821508D-9/
  DATA expcs(6)/ + .573859878630206601252990815262106D-12/
  DATA expcs(7)/ + .177574448591421511802306980226000D-14/
  DATA expcs(8)/ + .480799166842372422675950244533333D-17/
  DATA expcs(9)/ + .115716376881828572809260000000000D-19/
  DATA expcs(10)/ + .250650610255497719932458666666666D-22/
  DATA expcs(11)/ + .493571708140495828480000000000000D-25/
  DATA expcs(12)/ + .890929572740634240000000000000000D-28/
  DATA expcs(13)/ + .148448062907997866666666666666666D-30/
  DATA expcs(14)/ + .229678916630186666666666666666666D-33/
  DATA twon16(1)/0.0D0/
  DATA twon16(2)/ + .44273782427413840321966478739929D-1/
  DATA twon16(3)/ + .90507732665257659207010655760707D-1/
  DATA twon16(4)/ + .13878863475669165370383028384151D+0/
  DATA twon16(5)/ + .18920711500272106671749997056047D+0/
  DATA twon16(6)/ + .24185781207348404859367746872659D+0/
  DATA twon16(7)/ + .29683955465100966593375411779245D+0/
  DATA twon16(8)/ + .35425554693689272829801474014070D+0/
  DATA twon16(9)/ + .41421356237309504880168872420969D+0/
  DATA twon16(10)/ + .47682614593949931138690748037404D+0/
  DATA twon16(11)/ + .54221082540794082361229186209073D+0/
  DATA twon16(12)/ + .61049033194925430817952066735740D+0/
  DATA twon16(13)/ + .68179283050742908606225095246642D+0/
  DATA twon16(14)/ + .75625216037329948311216061937531D+0/
  DATA twon16(15)/ + .83400808640934246348708318958828D+0/
  DATA twon16(16)/ + .91520656139714729387261127029583D+0/
  DATA twon16(17)/1.0D0/
  DATA aln216/ + .83120654223414517758794896030274D-1/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  DEXP
  IF ( first ) THEN
    nterms = INITDS(expcs,14,0.1*SNGL(D1MACH(3)))
    xmin = DLOG(D1MACH(1)) + 0.01D0
    xmax = DLOG(D1MACH(2)) - 0.01D0
  ENDIF
  first = .FALSE.
  !
  IF ( X>=xmin ) THEN
    IF ( X>xmax ) CALL XERMSG('SLATEC','DEXP','X SO BIG DEXP OVERFLOWS',2,2)
    !
    xint = DINT(X)
    y = X - xint
    !
    y = 23.0D0*y + X*aln216
    n = INT( y )
    f = y - n
    n = INT( 23.0D0*xint ) + n
    n16 = n/16
    IF ( n<0 ) n16 = n16 - 1
    ndx = n - 16*n16 + 1
    !
    DEXP = 1.0D0 + (twon16(ndx)+f*(1.0D0+twon16(ndx))*DCSEVL(f,expcs,nterms)&
      )
    !
    DEXP = D9PAK(DEXP,n16)
  ELSE
    !
    CALL XERMSG('SLATEC','DEXP','X SO SMALL DEXP UNDERFLOWS',1,1)
    DEXP = 0.0D0
  ENDIF
  !
END FUNCTION DEXP
!DECK DINT
REAL(8) FUNCTION DINT(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DINT
  !***PURPOSE  Find the largest integer whose magnitude does not
  !            exceed the argument and convert to double precision.
  !***LIBRARY   FNLIB
  !***CATEGORY  C1
  !***TYPE      DOUBLE PRECISION (DINT-D)
  !***KEYWORDS  DINT, DOUBLE PRECISION INTEGER, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DINT, X, Y
  !      Y = DINT(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DINT.
  !
  ! *Function Return Values:
  !
  !    DINT    :    the truncation value of X.
  !
  ! *Description:
  !
  !    DINT evaluates the integer value of a double precision argument.
  !    The real and integer bases are compared.  If the bases are not
  !    equal, an error message is generated by XERMSG and the routine
  !    aborts.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, I1MACH, R1MACH, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   790801  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900525  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DINT
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: part, scale, xbig, xmax, xscl
  INTEGER i, ibase, ipart, ndigd, ndigi, npart
  LOGICAL first
  !     .. External Functions ..
  REAL(8) :: D1MACH
  REAL R1MACH
  INTEGER I1MACH
  EXTERNAL D1MACH, R1MACH, I1MACH
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC AMIN1, DABS, INT, MIN0, REAL, SNGL
  !     .. Save statement ..
  SAVE first, npart, scale, xbig, xmax
  !     .. Data statements ..
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT    DINT
  IF ( first ) THEN
    ibase = I1MACH(7)
    ndigd = I1MACH(14)
    ndigi = MIN0(I1MACH(8),I1MACH(11)) - 1
    xmax = 1.0D0/D1MACH(4)
    xbig = AMIN1(REAL(I1MACH(9)),1.0/R1MACH(4))
    IF ( ibase/=I1MACH(10) ) CALL XERMSG('SLATEC','DINT',&
      'ALGORITHM ERROR.  INTEGER BASE NE REAL BASE',2,2)
    npart = (ndigd+ndigi-1)/ndigi
    scale = ibase**ndigi
  ENDIF
  first = .FALSE.
  !
  IF ( X>=-xbig.AND.X<=xbig ) THEN
    DINT = INT(SNGL(X))
  ELSE
    xscl = DABS(X)
    IF ( xscl<=xmax ) THEN
      DO i = 1, npart
        xscl = xscl/scale
      ENDDO
      DINT = 0.
      DO i = 1, npart
        xscl = xscl*scale
        ipart = INT( xscl )
        part = ipart
        xscl = xscl - part
        DINT = DINT*scale + part
      ENDDO
      !
      IF ( X<0.0D0 ) DINT = -DINT
    ELSE
      CALL XERMSG('SLATEC','DINT',&
        'DABS(X) MAY BE TOO BIG TO BE REPRESENTED AS'//&
        'AN EXACT INTEGER',1,1)
      DINT = X
    ENDIF
  ENDIF
  !
END FUNCTION DINT
!DECK DLOG
REAL(8) FUNCTION DLOG(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DLOG
  !***PURPOSE  Compute the double precision natural logarithm.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4B
  !***TYPE      DOUBLE PRECISION (ALOG-S, DLOG-D, CLOG-C)
  !***KEYWORDS  DLOG, DOUBLE PRECISION NATURAL LOGARITHM,
  !             ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DLOG, X, Y
  !      Y = DLOG(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DLOG.
  !
  ! *Function Return Values:
  !
  !    DLOG    :    the double precision natural logarithm of X.
  !
  ! *Description:
  !
  !    DLOG evaluates the double precision natural logarithm of a double
  !    precision argument.  The argument X must be positive.  For a
  !    non-positive argument, an error message is generated by XERMSG
  !    and the routine aborts.
  !
  !    SERIES FOR ALN     ON THE INTERVAL  0.          TO  3.46021E-03
  !                                        WITH WEIGHTED ERROR   4.15E-32
  !                                         LOG WEIGHTED ERROR  31.38
  !                               SIGNIFICANT FIGURES REQUIRED  31.21
  !                                    DECIMAL PLACES REQUIRED  31.90
  !
  !    ALN2 = ALOG(2.0) - 0.625
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, D9UPAK, DCSEVL, INITDS, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DLOG
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: aln2, t, t2, xn, y
  INTEGER n, nterms, ntrval
  !     .. Local Arrays ..
  REAL(8) :: alncen(5), alncs(11), center(4)
  !     .. External Functions ..
  REAL(8) :: D1MACH, DCSEVL
  INTEGER INITDS
  EXTERNAL D1MACH, DCSEVL, INITDS
  !     .. External Subroutines ..
  EXTERNAL D9UPAK, XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC SNGL
  !     .. Save statement ..
  SAVE nterms
  !     .. Data statements ..
  DATA alncs(1)/ + .13347199877973881561689386047187D+1/
  DATA alncs(2)/ + .69375628328411286281372438354225D-3/
  DATA alncs(3)/ + .42934039020450834506559210803662D-6/
  DATA alncs(4)/ + .28933847795432594580466440387587D-9/
  DATA alncs(5)/ + .20512517530340580901741813447726D-12/
  DATA alncs(6)/ + .15039717055497386574615153319999D-15/
  DATA alncs(7)/ + .11294540695636464284521613333333D-18/
  DATA alncs(8)/ + .86355788671171868881946666666666D-22/
  DATA alncs(9)/ + .66952990534350370613333333333333D-25/
  DATA alncs(10)/ + .52491557448151466666666666666666D-28/
  DATA alncs(11)/ + .41530540680362666666666666666666D-31/
  DATA center(1)/1.0D0/
  DATA center(2)/1.25D0/
  DATA center(3)/1.50D0/
  DATA center(4)/1.75D0/
  DATA alncen(1)/0.0D0/
  DATA alncen(2)/ + .22314355131420975576629509030983D+0/
  DATA alncen(3)/ + .40546510810816438197801311546434D+0/
  DATA alncen(4)/ + .55961578793542268627088850052682D+0/
  DATA alncen(5)/ + .69314718055994530941723212145817D+0/
  DATA aln2/0.06814718055994530941723212145818D0/
  DATA nterms/0/
  !***FIRST EXECUTABLE STATEMENT  DLOG
  IF ( nterms==0 ) nterms = INITDS(alncs,11,28.9*SNGL(D1MACH(3)))
  !
  IF ( X<=0.0D0 ) CALL XERMSG('SLATEC','DLOG','X IS ZERO OR NEGATIVE',1,2)
  !
  CALL D9UPAK(X,y,n)
  !
  xn = n - 1
  y = 2.0D0*y
  ntrval = INT( 4.0D0*y - 2.5D0 )
  !
  IF ( ntrval==5 ) t = ((y-1.0D0)-1.0D0)/(y+2.0D0)
  IF ( ntrval<5 ) t = (y-center(ntrval))/(y+center(ntrval))
  t2 = t*t
  DLOG = 0.625D0*xn + &
    (aln2*xn+alncen(ntrval)+2.0D0*t+t*t2*DCSEVL(578.D0*t2-1.0D0,alncs,&
    nterms))
  !
END FUNCTION DLOG
!DECK DLOG10
REAL(8) FUNCTION DLOG10(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DLOG10
  !***PURPOSE  Compute the double precision common base 10 logarithm.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4B
  !***TYPE      DOUBLE PRECISION (ALOG10-S, DLOG10-D)
  !***KEYWORDS  DLOG10, DOUBLE PRECISION BASE 10 LOGARITHM,
  !             ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DLOG10, X, Y
  !      Y = DLOG10(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is an argument.  It will not be modified by
  !                 DLOG10.
  !
  ! *Function Return Values:
  !
  !    DLOG10  :    the double precision base 10, i.e. common, logarithm.
  !
  ! *Description:
  !
  !    DLOG10 evaluates the double precision base 10 logarithm of a double
  !    precision argument.  The argument X must be positive.  For a non-
  !    positive argument, an error message is generated by XERMSG and the
  !    routine aborts.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  DLOG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DLOG10
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: dloge
  !     .. External Functions ..
  REAL(8) :: DLOG
  EXTERNAL DLOG
  !     .. Data statements ..
  DATA dloge/0.43429448190325182765112891891661D0/
  !***FIRST EXECUTABLE STATEMENT  DLOG10
  DLOG10 = dloge*DLOG(X)
  !
END FUNCTION DLOG10
!DECK DSIN
REAL(8) FUNCTION DSIN(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DSIN
  !***PURPOSE  Compute the double precision sine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      DOUBLE PRECISION (SIN-S, DSIN-D, CSIN-C)
  !***KEYWORDS  DOUBLE PRECISION SINE, DSIN, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DSIN, X, Y
  !      Y = DSIN(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DSIN.
  !
  ! *Function Return Values:
  !
  !    DSIN    :    the double precision sine of a double precision
  !                 argument.
  !
  ! *Description:
  !
  !    DSIN evaluates the double precision sine of a double precision
  !    argument.  If the magnitude of the argument X is greater than the
  !    reciprocal of D1MACH(4), an error message is generated by XERMSG
  !    and the routine aborts.
  !
  !    SERIES FOR SIN     ON THE INTERVAL  0.          TO  1.00000E+00
  !                                        WITH WEIGHTED ERROR   7.08E-32
  !                                         LOG WEIGHTED ERROR  31.15
  !                               SIGNIFICANT FIGURES REQUIRED  29.74
  !                                    DECIMAL PLACES REQUIRED  31.69
  !
  !    SERIES FOR COS     ON THE INTERVAL  0.          TO  1.00000E+00
  !                                        WITH WEIGHTED ERROR   5.80E-34
  !                                         LOG WEIGHTED ERROR  33.24
  !                               SIGNIFICANT FIGURES REQUIRED  32.49
  !                                    DECIMAL PLACES REQUIRED  33.79
  !
  !    PI4REC = 4.0/PI - 1.0
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, DCSEVL, DINT, DSQRT, INITDS, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !   901219  Logic in code corrected.  (WRB)
  !***END PROLOGUE  DSIN
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: ainty, ainty2, pi4rec, xmax, xsml, xwarn, y, &
    yrem, z
  REAL eta
  INTEGER ifn, noctnt, ntcs, ntsn
  LOGICAL first
  !     .. Local Arrays ..
  REAL(8) :: coscs(13), sincs(12)
  !     .. External Functions ..
  REAL(8) :: D1MACH, DCSEVL, DINT, DSQRT
  INTEGER INITDS
  EXTERNAL D1MACH, DCSEVL, DINT, DSQRT, INITDS
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC ABS, DMOD, MOD
  !     .. Save statement ..
  SAVE first, ntsn, ntcs, xmax, xsml, xwarn
  !     .. Data statements ..
  DATA sincs(1)/ - .8104079085448715800196469943559D-2/
  DATA sincs(2)/ - .3914456752708195701742853890074D-1/
  DATA sincs(3)/ + .3045094206789444055815695590878D-3/
  DATA sincs(4)/ - .1123574976796415958214203624984D-5/
  DATA sincs(5)/ + .2414039972413749607105791078174D-8/
  DATA sincs(6)/ - .3391636705037535474000613712810D-11/
  DATA sincs(7)/ + .3358087618514203445901588366650D-14/
  DATA sincs(8)/ - .2468983320993182257103205610666D-17/
  DATA sincs(9)/ + .1401147833510729936175146666666D-20/
  DATA sincs(10)/ - .6322858930224709680426666666666D-24/
  DATA sincs(11)/ + .2323065248071953216000000000000D-27/
  DATA sincs(12)/ - .7083757638662399999999999999999D-31/
  DATA coscs(1)/ + .203263827409616025400812030121852D+0/
  DATA coscs(2)/ - .146436644390836863320796360139993D+0/
  DATA coscs(3)/ + .192144931181464679690714543745079D-2/
  DATA coscs(4)/ - .996496848982930006866910618423658D-5/
  DATA coscs(5)/ + .275765956071873951864383935301797D-7/
  DATA coscs(6)/ - .473994980816484403744229510321398D-10/
  DATA coscs(7)/ + .554954854148518274082726416274480D-13/
  DATA coscs(8)/ - .470970490651755595660385183093333D-16/
  DATA coscs(9)/ + .302989760807937313388574986666666D-19/
  DATA coscs(10)/ - .152841493421461533614912000000000D-22/
  DATA coscs(11)/ + .620745154357827253973333333333333D-26/
  DATA coscs(12)/ - .207333072298361599999999999999999D-29/
  DATA coscs(13)/ + .579538533367466666666666666666666D-33/
  DATA pi4rec/ + .27323954473516268615107010698011D0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  DSIN
  IF ( first ) THEN
    eta = REAL( 0.1D0*D1MACH(3), 4 )
    ntsn = INITDS(sincs,12,eta)
    ntcs = INITDS(coscs,13,eta)
    xsml = DSQRT(2.0D0*D1MACH(3))
    xmax = 1.0D0/D1MACH(4)
    xwarn = DSQRT(xmax)
  ENDIF
  first = .FALSE.
  !
  y = ABS(X)
  IF ( y>xmax ) CALL XERMSG('SLATEC','DSIN',&
    'NO PRECISION BECAUSE DABS(X) IS TOO BIG',2,2)
  IF ( y>xwarn ) CALL XERMSG('SLATEC','DSIN',&
    'ANSWER LT HALF PRECISION BECAUSE DABS(X) IS BIG'&
    ,1,1)
  !
  IF ( y<xsml ) THEN
    DSIN = X
  ELSE
    !
    !       CAREFULLY COMPUTE Y * (4/PI)
    !         = (AINT(Y) + REM(Y)) * (1. + PI4REC)
    !         = AINT(Y) + REM(Y) + Y*PI4REC
    !         = AINT(Y) + AINT(REM(Y)+Y*PI4REC) + REM(REM(Y)+Y*PI4REC)
    !
    ainty = DINT(y)
    yrem = y - ainty
    y = yrem + y*pi4rec
    ainty2 = DINT(y)
    ainty = ainty + ainty2
    y = y - ainty2
    !
    noctnt = INT( DMOD(ainty,8.0D0) )
    ifn = MOD((noctnt+1)/2,2)
    IF ( MOD(noctnt,2)==1 ) y = 1.0D0 - y
    z = 2.0D0*y*y - 1.0D0
    !
    IF ( ifn/=1 ) THEN
      DSIN = y*(0.75D0+DCSEVL(z,sincs,ntsn))
    ELSE
      DSIN = 0.75D0 + DCSEVL(z,coscs,ntcs)
    ENDIF
    IF ( noctnt>3 ) DSIN = -DSIN
    IF ( X<0.0D0 ) DSIN = -DSIN
  ENDIF
  !
END FUNCTION DSIN
!DECK DSINH
REAL(8) FUNCTION DSINH(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DSINH
  !***PURPOSE  Compute the double precision hyperbolic sine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4C
  !***TYPE      DOUBLE PRECISION (SINH-S, DSINH-D)
  !***KEYWORDS  DOUBLE PRECISION HYPERBOLIC SINE, DSINH,
  !             ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DSINH, X, Y
  !      Y = DSINH(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DSINH.
  !
  ! *Function Return Values:
  !
  !    DSINH   :    the double precision hyperbolic sine of X.
  !
  ! *Description:
  !
  !    DSINH evaluates the double precision hyperbolic sine of a double
  !    precision argument.
  !
  !    SERIES FOR SINH    ON THE INTERVAL  0.          TO  1.00000E+00
  !                                        WITH WEIGHTED ERROR   7.76E-33
  !                                         LOG WEIGHTED ERROR  32.11
  !                               SIGNIFICANT FIGURES REQUIRED  31.20
  !                                    DECIMAL PLACES REQUIRED  32.67
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, DCSEVL, DEXP, DSQRT, INITDS
  !***REVISION HISTORY  (YYMMDD)
  !   770801  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !   901219  Code corrected to compute DSINH correctly when ABS(X) .GE.
  !           YMAX.  (WRB)
  !***END PROLOGUE  DSINH
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: sqeps, y, ymax
  INTEGER nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL(8) :: sinhcs(13)
  !     .. External Functions ..
  REAL(8) :: D1MACH, DCSEVL, DEXP, DSQRT
  INTEGER INITDS
  EXTERNAL D1MACH, DCSEVL, DEXP, DSQRT, INITDS
  !     .. Intrinsic Functions ..
  INTRINSIC DABS, DSIGN, SNGL
  !     .. Save statement ..
  SAVE first, nterms, sqeps, ymax
  !     .. Data statements ..
  DATA sinhcs(1)/ + .17304219404717963167588384698501D+0/
  DATA sinhcs(2)/ + .87594221922760477154900263454440D-1/
  DATA sinhcs(3)/ + .10794777745671327502427270651579D-2/
  DATA sinhcs(4)/ + .63748492607547504815685554571850D-5/
  DATA sinhcs(5)/ + .22023664049230530159190496019502D-7/
  DATA sinhcs(6)/ + .49879401804158493149425807203661D-10/
  DATA sinhcs(7)/ + .79730535541157304814411480441186D-13/
  DATA sinhcs(8)/ + .94731587130725443342927317226666D-16/
  DATA sinhcs(9)/ + .86934920504480078871023098666666D-19/
  DATA sinhcs(10)/ + .63469394403318040457397333333333D-22/
  DATA sinhcs(11)/ + .37740337870858485738666666666666D-25/
  DATA sinhcs(12)/ + .18630213719570056533333333333333D-28/
  DATA sinhcs(13)/ + .77568437166506666666666666666666D-32/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  DSINH
  IF ( first ) THEN
    nterms = INITDS(sinhcs,13,0.1*SNGL(D1MACH(3)))
    sqeps = DSQRT(6.0D0*D1MACH(3))
    ymax = 1.0D0/DSQRT(D1MACH(3))
  ENDIF
  first = .FALSE.
  !
  y = DABS(X)
  IF ( y<=1.0D0 ) THEN
    DSINH = X
    IF ( y>sqeps ) DSINH = X*(1.0D0+DCSEVL(2.0D0*X*X-1.0D0,sinhcs,nterms))
  ELSE
    y = DEXP(y)
    IF ( y>=ymax ) THEN
      DSINH = DSIGN(0.5D0*y,X)
    ELSE
      DSINH = DSIGN(0.5D0*(y-1.0D0/y),X)
    ENDIF
  ENDIF
  !
END FUNCTION DSINH
!DECK DSQRT
REAL(8) FUNCTION DSQRT(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DSQRT
  !***PURPOSE  Compute the double precision square root.
  !***LIBRARY   FNLIB
  !***CATEGORY  C2
  !***TYPE      DOUBLE PRECISION (SQRT-S, DSQRT-D, CSQRT-C)
  !***KEYWORDS  DOUBLE PRECISION SQUARE ROOT, DSQRT, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DSQRT, X, Y
  !      Y = DSQRT(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DSQRT.
  !
  ! *Function Return Values:
  !
  !    DSQRT   :    the double precision square root of X.
  !
  ! *Description:
  !
  !    DSQRT evaluates the double precision square root of a double
  !    precision argument.  The argument X must be positive.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  ALOG, D1MACH, D9PAK, D9UPAK, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DSQRT
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: y
  REAL z
  INTEGER irem, iter, ixpnt, n, niter
  !     .. Local Arrays ..
  REAL(8) :: sqrt2(3)
  !     .. External Functions ..
  REAL(8) :: D1MACH, D9PAK
  REAL ALOG
  EXTERNAL D1MACH, D9PAK, ALOG
  !     .. External Subroutines ..
  EXTERNAL D9UPAK, XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC SNGL
  !     .. Save statement ..
  SAVE niter
  !     .. Data statements ..
  DATA sqrt2(1)/0.70710678118654752440084436210485D0/
  DATA sqrt2(2)/1.0D0/
  DATA sqrt2(3)/1.41421356237309504880168872420970D0/
  DATA niter/0/
  !***FIRST EXECUTABLE STATEMENT  DSQRT
  IF ( niter==0 ) niter = INT( 1.443*ALOG(-0.104*ALOG(0.1*SNGL(D1MACH(3)))) ) + 1
  !
  IF ( X>0.0D0 ) THEN
    !
    CALL D9UPAK(X,y,n)
    ixpnt = n/2
    irem = n - 2*ixpnt + 2
    !
    ! THE APPROXIMATION BELOW HAS ACCURACY OF 4.16 DIGITS.
    !
    z = REAL( y, 4 )
    DSQRT = 0.261599E0 + z*(1.114292E0+z*(-0.516888E0+z*0.141067E0))
    !
    DO iter = 1, niter
      DSQRT = DSQRT + 0.5D0*(y-DSQRT*DSQRT)/DSQRT
    ENDDO
    !
    DSQRT = D9PAK(sqrt2(irem)*DSQRT,ixpnt)
  ELSE
    DSQRT = 0.0D0
    IF ( X<0.0D0 ) CALL XERMSG('SLATEC','DSQRT','X IS NEGATIVE',1,1)
  ENDIF
  !
END FUNCTION DSQRT
!DECK DTAN
REAL(8) FUNCTION DTAN(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DTAN
  !***PURPOSE  Compute the double precision tangent.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      DOUBLE PRECISION (TAN-S, DTAN-D)
  !***KEYWORDS  DOUBLE PRECISION TANGENT, DTAN, ELEMENTARY FUNCTION,
  !             FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DTAN, X, Y
  !      Y = DTAN(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DTAN.
  !
  ! *Function Return Values:
  !
  !    DTAN    :    the double precision tangent of X.
  !
  ! *Description:
  !
  !    DTAN evaluates the double precision tangent of a double precision
  !    argument.  If the magnitude of the argument X is greater than
  !    the reciprocal of D1MACH(4), an error message is generated by
  !    XERMSG and the routine aborts.
  !
  !    SERIES FOR TAN     ON THE INTERVAL  0.          TO  6.25000E-02
  !                                        WITH WEIGHTED ERROR   1.44E-32
  !                                         LOG WEIGHTED ERROR  31.84
  !                               SIGNIFICANT FIGURES REQUIRED  30.92
  !                                    DECIMAL PLACES REQUIRED  32.48
  !
  !    PI2REC = 2/PI - 0.625
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, DCSEVL, DINT, DSQRT, INITDS, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DTAN
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: ainty, ainty2, pi2rec, prodbg, sqeps, xmax, xsml, &
    y, yrem
  INTEGER ifn, nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL(8) :: tancs(19)
  !     .. External Functions ..
  REAL(8) :: D1MACH, DCSEVL, DINT, DSQRT
  INTEGER INITDS
  EXTERNAL D1MACH, DCSEVL, DINT, DSQRT, INITDS
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC DABS, DMOD, DSIGN, SNGL
  !     .. Save statement ..
  SAVE first, nterms, sqeps, xmax, xsml
  !     .. Data statements ..
  DATA tancs(1)/ + .22627932763129357846578636531752D+0/
  DATA tancs(2)/ + .43017913146548961775583410748067D-1/
  DATA tancs(3)/ + .68544610682565088756929473623461D-3/
  DATA tancs(4)/ + .11045326947597098383578849369696D-4/
  DATA tancs(5)/ + .17817477903926312943238512588940D-6/
  DATA tancs(6)/ + .28744968582365265947529646832471D-8/
  DATA tancs(7)/ + .46374854195902995494137478234363D-10/
  DATA tancs(8)/ + .74817609041556138502341633308215D-12/
  DATA tancs(9)/ + .12070497002957544801644516947824D-13/
  DATA tancs(10)/ + .19473610812823019305513858584533D-15/
  DATA tancs(11)/ + .31417224874732446504614586026666D-17/
  DATA tancs(12)/ + .50686132555800153941904891733333D-19/
  DATA tancs(13)/ + .81773105159836540043979946666666D-21/
  DATA tancs(14)/ + .13192643412147384408951466666666D-22/
  DATA tancs(15)/ + .21283995497042377309866666666666D-24/
  DATA tancs(16)/ + .34337960192345945292800000000000D-26/
  DATA tancs(17)/ + .55398222121173811200000000000000D-28/
  DATA tancs(18)/ + .89375227794352810666666666666666D-30/
  DATA tancs(19)/ + .14419111371369130666666666666666D-31/
  DATA pi2rec/.011619772367581343075535053490057D0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  DTAN
  IF ( first ) THEN
    nterms = INITDS(tancs,19,0.1*SNGL(D1MACH(3)))
    xmax = 1.0D0/D1MACH(4)
    xsml = DSQRT(3.0D0*D1MACH(3))
    sqeps = DSQRT(D1MACH(4))
  ENDIF
  first = .FALSE.
  !
  y = DABS(X)
  IF ( y>xmax ) CALL XERMSG('SLATEC','DTAN',&
    'NO PRECISION BECAUSE DABS(X) IS TOO BIG',2,2)
  !
  !     CAREFULLY COMPUTE Y * (2/PI)
  !       = (AINT(Y) + REM(Y)) * (.625 + PI2REC)
  !       = AINT(.625*Y) + REM(.625*Y) + Y*PI2REC  =  AINT(.625*Y) + Z
  !       = AINT(.625*Y) + AINT(Z) + REM(Z)
  !
  ainty = DINT(y)
  yrem = y - ainty
  prodbg = 0.625D0*ainty
  ainty = DINT(prodbg)
  y = (prodbg-ainty) + 0.625D0*yrem + pi2rec*y
  ainty2 = DINT(y)
  ainty = ainty + ainty2
  y = y - ainty2
  !
  ifn = INT( DMOD(ainty,2.0D0) )
  IF ( ifn==1 ) y = 1.0D0 - y
  IF ( 1.0D0-y<DABS(X)*sqeps ) CALL XERMSG('SLATEC','DTAN',&
    'ANSWER LT HALF PRECISION, DABS(X) BIG OR NEAR PI/2 OR 3*PI/2',1,1)
  IF ( y==1.0D0 ) CALL XERMSG('SLATEC','DTAN','X IS PI/2 OR 3*PI/2',3,2)
  !
  IF ( y<=0.25D0 ) THEN
    DTAN = X
    IF ( y>xsml ) DTAN = y*(1.5D0+DCSEVL(32.D0*y*y-1.0D0,tancs,nterms))
    !
  ELSEIF ( y<=0.5D0 ) THEN
    DTAN = 0.5D0*y*(1.5D0+DCSEVL(8.0D0*y*y-1.0D0,tancs,nterms))
    DTAN = 2.0*DTAN/(1.0D0-DTAN*DTAN)
  ELSE
    !
    DTAN = 0.25D0*y*(1.5D0+DCSEVL(2.D0*y*y-1.0D0,tancs,nterms))
    DTAN = 2.0D0*DTAN/(1.0D0-DTAN*DTAN)
    DTAN = 2.0D0*DTAN/(1.0D0-DTAN*DTAN)
    !
  ENDIF
  IF ( X/=0.0D0 ) DTAN = DSIGN(DTAN,X)
  IF ( ifn==1 ) DTAN = -DTAN
  !
END FUNCTION DTAN
!DECK DTANH
REAL(8) FUNCTION DTANH(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  DTANH
  !***PURPOSE  Compute the double precision hyperbolic tangent.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4C
  !***TYPE      DOUBLE PRECISION (TANH-S, DTANH-D)
  !***KEYWORDS  DOUBLE PRECISION HYPERBOLIC TANGENT, DTANH,
  !             ELEMENTARY FUNCTION, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION DTANH, X, Y
  !      Y = DTANH(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 DTANH.
  !
  ! *Function Return Values:
  !
  !    DTANH   :    the double precision hyperbolic tangent of X.
  !
  !
  ! *Description:
  !
  !    DTANH evaluates the double precision hyperbolic tangent of a
  !    double precision argument.
  !
  !    SERIES FOR TANH    ON THE INTERVAL  0.          TO  1.00000E+00
  !                                        WITH WEIGHTED ERROR   9.92E-33
  !                                         LOG WEIGHTED ERROR  32.00
  !                               SIGNIFICANT FIGURES REQUIRED  31.25
  !                                    DECIMAL PLACES REQUIRED  32.75
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  D1MACH, DCSEVL, DEXP, DLOG, DSQRT, INITDS
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900524  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  DTANH
  !     .. Scalar Arguments ..
  REAL(8) :: X
  !     .. Local Scalars ..
  REAL(8) :: sqeps, xmax, y
  INTEGER nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL(8) :: tanhcs(31)
  !     .. External Functions ..
  REAL(8) :: D1MACH, DCSEVL, DEXP, DLOG, DSQRT
  INTEGER INITDS
  EXTERNAL D1MACH, DCSEVL, DEXP, DLOG, DSQRT, INITDS
  !     .. Intrinsic Functions ..
  INTRINSIC DABS, DSIGN, SNGL
  !     .. Save statement ..
  SAVE first, nterms, sqeps, xmax
  !     .. Data statements ..
  DATA tanhcs(1)/ - .25828756643634710438338151450605D+0/
  DATA tanhcs(2)/ - .11836106330053496535383671940204D+0/
  DATA tanhcs(3)/ + .98694426480063988762827307999681D-2/
  DATA tanhcs(4)/ - .83579866234458257836163690398638D-3/
  DATA tanhcs(5)/ + .70904321198943582626778034363413D-4/
  DATA tanhcs(6)/ - .60164243181207040390743479001010D-5/
  DATA tanhcs(7)/ + .51052419080064402965136297723411D-6/
  DATA tanhcs(8)/ - .43320729077584087216545467387192D-7/
  DATA tanhcs(9)/ + .36759990553445306144930076233714D-8/
  DATA tanhcs(10)/ - .31192849612492011117215651480953D-9/
  DATA tanhcs(11)/ + .26468828199718962579377758445381D-10/
  DATA tanhcs(12)/ - .22460239307504140621870997006196D-11/
  DATA tanhcs(13)/ + .19058733768288196054319468396139D-12/
  DATA tanhcs(14)/ - .16172371446432292391330769279701D-13/
  DATA tanhcs(15)/ + .13723136142294289632897761289386D-14/
  DATA tanhcs(16)/ - .11644826870554194634439647293781D-15/
  DATA tanhcs(17)/ + .98812684971669738285540514338133D-17/
  DATA tanhcs(18)/ - .83847933677744865122269229055999D-18/
  DATA tanhcs(19)/ + .71149528869124351310723506176000D-19/
  DATA tanhcs(20)/ - .60374242229442045413288837119999D-20/
  DATA tanhcs(21)/ + .51230825877768084883404663466666D-21/
  DATA tanhcs(22)/ - .43472140157782110106047829333333D-22/
  DATA tanhcs(23)/ + .36888473639031328479423146666666D-23/
  DATA tanhcs(24)/ - .31301874774939399883325439999999D-24/
  DATA tanhcs(25)/ + .26561342006551994468488533333333D-25/
  DATA tanhcs(26)/ - .22538742304145029883494399999999D-26/
  DATA tanhcs(27)/ + .19125347827973995102208000000000D-27/
  DATA tanhcs(28)/ - .16228897096543663117653333333333D-28/
  DATA tanhcs(29)/ + .13771101229854738786986666666666D-29/
  DATA tanhcs(30)/ - .11685527840188950118399999999999D-30/
  DATA tanhcs(31)/ + .99158055384640389120000000000000D-32/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  DTANH
  IF ( first ) THEN
    nterms = INITDS(tanhcs,31,0.1*SNGL(D1MACH(3)))
    sqeps = DSQRT(3.0D0*D1MACH(3))
    xmax = -0.5D0*DLOG(D1MACH(3))
  ENDIF
  first = .FALSE.
  !
  y = DABS(X)
  IF ( y<=1.0D0 ) THEN
    DTANH = X
    IF ( y>sqeps ) DTANH = X*(1.0D0+DCSEVL(2.0D0*X*X-1.0D0,tanhcs,nterms))
  ELSEIF ( y<=xmax ) THEN
    y = DEXP(X)
    DTANH = DSIGN((y-1.0D0/y)/(y+1.0D0/y),X)
  ELSE
    DTANH = DSIGN(1.0D0,X)
  ENDIF
  !
END FUNCTION DTANH
!DECK EXP
REAL FUNCTION EXP(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  EXP
  !***PURPOSE  Compute the exponential.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4B
  !***TYPE      SINGLE PRECISION (EXP-S, DEXP-D, CEXP-C)
  !***KEYWORDS  ELEMENTARY FUNCTION, EXP, FORTRAN INTRINSIC
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL EXP, X, Y
  !      Y = EXP(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 EXP.
  !
  ! *Function Return Values:
  !
  !    EXP     :    the exponential of X.
  !
  ! *Description:
  !
  !    EXP evaluates the exponential of an argument, i.e. exp(Z) or
  !    e**Z.  If the argument X is too big (ln(R1MACH(2)) - 0.01),
  !    an error message is generated by XERMSG and the routine aborts.
  !
  !    SERIES FOR EXP     ON THE INTERVAL -1.00000D+00 TO  1.00000D+00
  !                                        WITH WEIGHTED ERROR   4.81E-18
  !                                         LOG WEIGHTED ERROR  17.32
  !                               SIGNIFICANT FIGURES REQUIRED  15.95
  !                                    DECIMAL PLACES REQUIRED  17.77
  !
  !    ALN216 IS 16.0/ALOG(2.) - 23.0
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  ALOG, CSEVL, INITS, R1MACH, R9PAK, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900525  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  EXP
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL aln216, f, xint, xmax, xmin, y
  INTEGER n, n16, ndx, nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL expcs(8), twon16(17)
  !     .. External Functions ..
  REAL ALOG, CSEVL, R1MACH, R9PAK
  INTEGER INITS
  EXTERNAL ALOG, CSEVL, R1MACH, R9PAK, INITS
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC AINT
  !     .. Save statement ..
  SAVE first, nterms, xmin, xmax
  !     .. Data statements ..
  DATA expcs(1)/.086656949331498571E0/
  DATA expcs(2)/.000938494869299839E0/
  DATA expcs(3)/.000006776039709981E0/
  DATA expcs(4)/.000000036693120039E0/
  DATA expcs(5)/.000000000158959053E0/
  DATA expcs(6)/.000000000000573859E0/
  DATA expcs(7)/.000000000000001775E0/
  DATA expcs(8)/.000000000000000004E0/
  DATA twon16(1)/0.0E0/
  DATA twon16(2)/.44273782427413840E-1/
  DATA twon16(3)/.90507732665257659E-1/
  DATA twon16(4)/.13878863475669165E0/
  DATA twon16(5)/.18920711500272107E0/
  DATA twon16(6)/.24185781207348405E0/
  DATA twon16(7)/.29683955465100967E0/
  DATA twon16(8)/.35425554693689273E0/
  DATA twon16(9)/.41421356237309505E0/
  DATA twon16(10)/.47682614593949931E0/
  DATA twon16(11)/.54221082540794082E0/
  DATA twon16(12)/.61049033194925431E0/
  DATA twon16(13)/.68179283050742909E0/
  DATA twon16(14)/.75625216037329948E0/
  DATA twon16(15)/.83400808640934246E0/
  DATA twon16(16)/.91520656139714729E0/
  DATA twon16(17)/1.0E0/
  DATA aln216/.083120654223414518E0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  EXP
  IF ( first ) THEN
    nterms = INITS(expcs,8,0.1*R1MACH(3))
    xmin = ALOG(R1MACH(1)) + 0.01E0
    xmax = ALOG(R1MACH(2)) - 0.01E0
  ENDIF
  first = .FALSE.
  !
  IF ( X>=xmin ) THEN
    IF ( X>xmax ) CALL XERMSG('SLATEC','EXP','X SO BIG EXP OVERFLOWS',2,2)
    !
    xint = AINT(X)
    y = X - xint
    !
    y = 23.0*y + X*aln216
    n = INT( y )
    f = y - n
    n = INT( 23.0*xint ) + n
    n16 = n/16
    IF ( n<0 ) n16 = n16 - 1
    ndx = n - 16*n16 + 1
    !
    EXP = 1.0 + (twon16(ndx)+f*(1.0+twon16(ndx))*CSEVL(f,expcs,nterms))
    !
    EXP = R9PAK(EXP,n16)
  ELSE
    !
    CALL XERMSG('SLATEC','EXP','X SO SMALL EXP UNDERFLOWS',1,1)
    EXP = 0.0
  ENDIF
  !
END FUNCTION EXP
!DECK SIN
REAL FUNCTION SIN(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  SIN
  !***PURPOSE  Compute the sine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      SINGLE PRECISION (SIN-S, DSIN-D, CSIN-C)
  !***KEYWORDS  ELEMENTARY FUNCTION, FORTRAN INTRINSIC, SIN, SINE
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL SIN, X, Y
  !      Y = SIN(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 SIN.
  !
  ! *Function Return Values:
  !
  !    SIN     :    the sine of a X.
  !
  ! *Description:
  !
  !    SIN evaluates the sine of an argument.  If the argument X is
  !    greater than the reciprocal of R1MACH(4), an error message is
  !    generated by XERMSG and the routine aborts.
  !
  !    SERIES FOR SIN     ON THE INTERVAL  0.          TO  1.00000D+00
  !                                        WITH WEIGHTED ERROR   2.47E-18
  !                                         LOG WEIGHTED ERROR  17.61
  !                               SIGNIFICANT FIGURES REQUIRED  16.20
  !                                    DECIMAL PLACES REQUIRED  18.06
  !
  !    SERIES FOR COS     ON THE INTERVAL  0.          TO  1.00000D+00
  !                                        WITH WEIGHTED ERROR   4.71E-17
  !                                         LOG WEIGHTED ERROR  16.33
  !                               SIGNIFICANT FIGURES REQUIRED  15.58
  !                                    DECIMAL PLACES REQUIRED  16.78
  !
  !    PI4REC = 4.0/PI - 1.0
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  CSEVL, INITS, R1MACH, SQRT, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900525  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  SIN
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL ainty, ainty2, pi4rec, xmax, xsml, xwarn, y, yrem, z
  INTEGER ifn, noctnt, ntcs, ntsn
  LOGICAL first
  !     .. Local Arrays ..
  REAL coscs(8), sincs(8)
  !     .. External Functions ..
  REAL CSEVL, R1MACH, SQRT
  INTEGER INITS
  EXTERNAL CSEVL, R1MACH, SQRT, INITS
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC ABS, AINT, AMOD, MOD
  !     .. Save statement ..
  SAVE first, ntsn, ntcs, xmax, xsml, xwarn
  !     .. Data statements ..
  DATA sincs(1)/ - .008104079085448715E0/
  DATA sincs(2)/ - .039144567527081957E0/
  DATA sincs(3)/.000304509420678944E0/
  DATA sincs(4)/ - .000001123574976796E0/
  DATA sincs(5)/.000000002414039972E0/
  DATA sincs(6)/ - .000000000003391636E0/
  DATA sincs(7)/.000000000000003358E0/
  DATA sincs(8)/ - .000000000000000002E0/
  DATA coscs(1)/.20326382740961603E0/
  DATA coscs(2)/ - .14643664439083686E0/
  DATA coscs(3)/.001921449311814646E0/
  DATA coscs(4)/ - .000009964968489829E0/
  DATA coscs(5)/.000000027576595607E0/
  DATA coscs(6)/ - .000000000047399498E0/
  DATA coscs(7)/.000000000000055495E0/
  DATA coscs(8)/ - .000000000000000047E0/
  DATA pi4rec/ + .273239544735162686E0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  SIN
  IF ( first ) THEN
    ntsn = INITS(sincs,8,0.1*R1MACH(3))
    ntcs = INITS(coscs,8,0.1*R1MACH(3))
    xsml = SQRT(6.0E0*R1MACH(3))
    xmax = 1.0E0/R1MACH(4)
    xwarn = SQRT(xmax)
  ENDIF
  first = .FALSE.
  !
  y = ABS(X)
  IF ( y>xmax ) CALL XERMSG('SLATEC','SIN',&
    'NO PRECISION BECAUSE ABS(X) IS TOO BIG',2,2)
  IF ( y>xwarn ) CALL XERMSG('SLATEC','SIN',&
    'ANSWER LT HALF PRECISION BECAUSE ABS(X) IS BIG'&
    ,1,1)
  !
  IF ( y<xsml ) THEN
    SIN = X
  ELSE
    !
    !       CAREFULLY COMPUTE Y * (4/PI)
    !         = (AINT(Y) + REM(Y)) * (1. + PI4REC)
    !         = AINT(Y) + REM(Y) + Y*PI4REC
    !         = AINT(Y) + AINT(REM(Y)+Y*PI4REC) + REM(REM(Y)+Y*PI4REC)
    !
    ainty = AINT(y)
    yrem = y - ainty
    y = yrem + y*pi4rec
    ainty2 = AINT(y)
    ainty = ainty + ainty2
    y = y - ainty2
    !
    noctnt = INT( AMOD(ainty,8.0E0) )
    ifn = MOD((noctnt+1)/2,2)
    IF ( MOD(noctnt,2)==1 ) y = 1.0E0 - y
    z = 2.0E0*y**2 - 1.0E0
    !
    IF ( ifn/=1 ) THEN
      SIN = y*(0.75E0+CSEVL(z,sincs,ntsn))
    ELSE
      SIN = 0.75E0 + CSEVL(z,coscs,ntcs)
    ENDIF
    IF ( noctnt>3 ) SIN = -SIN
    IF ( X<0.0E0 ) SIN = -SIN
  ENDIF
  !
END FUNCTION SIN
!DECK SINH
REAL FUNCTION SINH(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  SINH
  !***PURPOSE  Compute the hyperbolic sine.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4C
  !***TYPE      SINGLE PRECISION (SINH-S, DSINH-D)
  !***KEYWORDS  ELEMENTARY FUNCTION, FORTRAN INTRINSIC, HYPERBOLIC SINE,
  !             SINH
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL SINH, X, Y
  !      Y = SINH(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 SINH.
  !
  ! *Function Return Values:
  !
  !    SINH    :    the hyperbolic sine of a complex argument.
  !
  ! *Description:
  !
  !    SINH evaluates the hyperbolic sine of an argument.
  !
  !    SERIES FOR SINH    ON THE INTERVAL  0.          TO  1.00000D+00
  !                                        WITH WEIGHTED ERROR   9.47E-17
  !                                         LOG WEIGHTED ERROR  16.02
  !                               SIGNIFICANT FIGURES REQUIRED  15.11
  !                                    DECIMAL PLACES REQUIRED  16.48
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  CSEVL, EXP, INITS, R1MACH, SQRT
  !***REVISION HISTORY  (YYMMDD)
  !   770801  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900525  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  SINH
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL sqeps, y, ymax
  INTEGER nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL sinhcs(8)
  !     .. External Functions ..
  REAL CSEVL, EXP, R1MACH, SQRT
  INTEGER INITS
  EXTERNAL CSEVL, EXP, R1MACH, SQRT, INITS
  !     .. Intrinsic Functions ..
  INTRINSIC ABS, SIGN
  !     .. Save statement ..
  SAVE first, nterms, sqeps, ymax
  !     .. Data statements ..
  DATA sinhcs(1)/.1730421940471796E0/
  DATA sinhcs(2)/.08759422192276048E0/
  DATA sinhcs(3)/.00107947777456713E0/
  DATA sinhcs(4)/.00000637484926075E0/
  DATA sinhcs(5)/.00000002202366404E0/
  DATA sinhcs(6)/.00000000004987940E0/
  DATA sinhcs(7)/.00000000000007973E0/
  DATA sinhcs(8)/.00000000000000009E0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  SINH
  IF ( first ) THEN
    nterms = INITS(sinhcs,8,0.1*R1MACH(3))
    sqeps = SQRT(6.0E0*R1MACH(3))
    ymax = 1.0E0/SQRT(R1MACH(3))
  ENDIF
  first = .FALSE.
  !
  y = ABS(X)
  IF ( y<=1.0E0 ) THEN
    SINH = X
    IF ( y>sqeps ) SINH = X*(1.0E0+CSEVL(2.0E0*X*X-1.0E0,sinhcs,nterms))
  ELSE
    y = EXP(y)
    IF ( y>=ymax ) THEN
      SINH = SIGN(0.5E0*y,X)
    ELSE
      SINH = SIGN(0.5E0*(y-1.0E0/y),X)
    ENDIF
  ENDIF
  !
END FUNCTION SINH
!DECK SQRT
REAL FUNCTION SQRT(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  SQRT
  !***PURPOSE  Compute the square root.
  !***LIBRARY   FNLIB
  !***CATEGORY  C2
  !***TYPE      SINGLE PRECISION (SQRT-S, DSQRT-D, CSQRT-C)
  !***KEYWORDS  ELEMENTARY FUNCTION, FORTRAN INTRINSIC, SQRT, SQUARE ROOT
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL SQRT, X, Y
  !      Y = SQRT(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 SQRT.
  !
  ! *Function Return Values:
  !
  !    SQRT    :    the square root of X.
  !
  ! *Description:
  !
  !    SQRT evaluates the square root of an argument.  The argument X
  !    must be positive.
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  ALOG, R1MACH, R9PAK, R9UPAK, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900525  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  SQRT
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL y
  INTEGER irem, iter, ixpnt, n, niter
  !     .. Local Arrays ..
  REAL sqrt2(3)
  !     .. External Functions ..
  REAL ALOG, R1MACH, R9PAK
  EXTERNAL ALOG, R1MACH, R9PAK
  !     .. External Subroutines ..
  EXTERNAL R9UPAK, XERMSG
  !     .. Save statement ..
  SAVE niter
  !     .. Data statements ..
  DATA sqrt2(1)/0.70710678118654752E0/
  DATA sqrt2(2)/1.0E0/
  DATA sqrt2(3)/1.41421356237309505E0/
  DATA niter/0/
  !***FIRST EXECUTABLE STATEMENT  SQRT
  IF ( niter==0 ) niter = INT( 1.443*ALOG(-0.104*ALOG(0.1*R1MACH(3))) ) + 1
  !
  IF ( X>0.0E0 ) THEN
    !
    CALL R9UPAK(X,y,n)
    ixpnt = n/2
    irem = n - 2*ixpnt + 2
    !
    ! THE APPROXIMATION BELOW HAS ACCURACY OF 4.16 DIGITS.
    !
    SQRT = .261599E0 + y*(1.114292E0+y*(-.516888E0+y*.141067E0))
    !
    DO iter = 1, niter
      SQRT = SQRT + 0.5E0*(y-SQRT**2)/SQRT
    ENDDO
    !
    SQRT = R9PAK(sqrt2(irem)*SQRT,ixpnt)
    !
  ELSE
    SQRT = 0.0E0
    IF ( X<0.0E0 ) CALL XERMSG('SLATEC','SQRT','X IS NEGATIVE',1,1)
  ENDIF
  !
END FUNCTION SQRT
!DECK TAN
REAL FUNCTION TAN(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  TAN
  !***PURPOSE  Compute the tangent.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4A
  !***TYPE      SINGLE PRECISION (TAN-S, DTAN-D)
  !***KEYWORDS  ELEMENTARY FUNCTION, FORTRAN INTRINSIC, TAN, TANGENT
  !***AUTHOR  FULLERTON, W., C-3, LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      DOUBLE PRECISION TAN, X, Y
  !      Y = TAN(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 TAN.
  !
  ! *Function Return Values:
  !
  !    TAN     :    the tangent of X.
  !
  ! *Description:
  !
  !    TAN evaluates the tangent of an argument.  If the magnitude of the
  !    argument X is greater than the reciprocal of D1MACH(4), an error
  !    message is generated and the routine aborts.
  !
  !    SERIES FOR TAN     ON THE INTERVAL  0.          TO  6.25000D-02
  !                                        WITH WEIGHTED ERROR   3.14E-18
  !                                         LOG WEIGHTED ERROR  17.50
  !                               SIGNIFICANT FIGURES REQUIRED  16.59
  !                                    DECIMAL PLACES REQUIRED  18.02
  !
  !    PI2REC = 2/PI - 0.625
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  CSEVL, INITS, R1MACH, SQRT, XERMSG
  !***REVISION HISTORY  (YYMMDD)
  !   770601  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900525  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  TAN
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL ainty, ainty2, pi2rec, prodbg, sqeps, xmax, xsml, y, yrem
  INTEGER ifn, nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL tancs(11)
  !     .. External Functions ..
  REAL CSEVL, R1MACH, SQRT
  INTEGER INITS
  EXTERNAL CSEVL, R1MACH, SQRT, INITS
  !     .. External Subroutines ..
  EXTERNAL XERMSG
  !     .. Intrinsic Functions ..
  INTRINSIC ABS, AINT, AMOD, SIGN
  !     .. Save statement ..
  SAVE first, nterms, sqeps, xmax, xsml
  !     .. Data statements ..
  DATA tancs(1)/.226279327631293578E0/
  DATA tancs(2)/.0430179131465489618E0/
  DATA tancs(3)/.0006854461068256508E0/
  DATA tancs(4)/.0000110453269475970E0/
  DATA tancs(5)/.0000001781747790392E0/
  DATA tancs(6)/.0000000028744968582E0/
  DATA tancs(7)/.0000000000463748541E0/
  DATA tancs(8)/.0000000000007481760E0/
  DATA tancs(9)/.0000000000000120704E0/
  DATA tancs(10)/.0000000000000001947E0/
  DATA tancs(11)/.0000000000000000031E0/
  DATA pi2rec/.0116197723675813430E0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  TAN
  IF ( first ) THEN
    nterms = INITS(tancs,11,0.1*R1MACH(3))
    xmax = 1.0E0/R1MACH(4)
    xsml = SQRT(3.0E0*R1MACH(3))
    sqeps = SQRT(R1MACH(4))
  ENDIF
  first = .FALSE.
  !
  y = ABS(X)
  IF ( y>xmax ) CALL XERMSG('SLATEC','TAN',&
    'NO PRECISION BECAUSE ABS(X) IS TOO BIG',2,2)
  !
  !     CAREFULLY COMPUTE Y * (2/PI)
  !       = (AINT(Y) + REM(Y)) * (.625 + PI2REC)
  !       = AINT(.625*Y) + REM(.625*Y) + Y*PI2REC  =  AINT(.625*Y) + Z
  !       = AINT(.625*Y) + AINT(Z) + REM(Z)
  !
  ainty = AINT(y)
  yrem = y - ainty
  prodbg = 0.625E0*ainty
  ainty = AINT(prodbg)
  y = (prodbg-ainty) + 0.625E0*yrem + y*pi2rec
  ainty2 = AINT(y)
  ainty = ainty + ainty2
  y = y - ainty2
  !
  ifn = INT( AMOD(ainty,2.0E0) )
  IF ( ifn==1 ) y = 1.0E0 - y
  !
  IF ( 1.0E0-y<ABS(X)*sqeps ) CALL XERMSG('SLATEC','TAN',&
    'ANSWER LT HALF PRECISION, ABS(X) BIG OR NEAR PI/2 OR 3*PI/2',1,1)
  IF ( y==1.0E0 ) CALL XERMSG('SLATEC','TAN','X IS PI/2 OR 3*PI/2',3,2)
  !
  IF ( y<=0.25E0 ) THEN
    TAN = X
    IF ( y>xsml ) TAN = y*(1.5E0+CSEVL(32.0E0*y**2-1.0E0,tancs,nterms))
    !
  ELSEIF ( y<=0.5E0 ) THEN
    TAN = 0.5E0*y*(1.5E0+CSEVL(8.0E0*y**2-1.0E0,tancs,nterms))
    TAN = 2.0E0*TAN/(1.0E0-TAN**2)
  ELSE
    !
    TAN = 0.25E0*y*(1.5E0+CSEVL(2.0E0*y**2-1.0E0,tancs,nterms))
    TAN = 2.0E0*TAN/(1.0E0-TAN**2)
    TAN = 2.0E0*TAN/(1.0E0-TAN**2)
    !
  ENDIF
  IF ( X/=0.0E0 ) TAN = SIGN(TAN,X)
  IF ( ifn==1 ) TAN = -TAN
  !
END FUNCTION TAN
!DECK TANH
REAL FUNCTION TANH(X)
  IMPLICIT NONE
  !***BEGIN PROLOGUE  TANH
  !***PURPOSE  Compute the hyperbolic tangent.
  !***LIBRARY   FNLIB
  !***CATEGORY  C4C
  !***TYPE      SINGLE PRECISION (TANH-S, DTANH-D)
  !***KEYWORDS  ELEMENTARY FUNCTION, FORTRAN INTRINSIC,
  !             HYPERBOLIC TANGENT, TANH
  !***AUTHOR  FULLERTON, W., C-3. LOS ALAMOS SCIENTIFIC LABORATORY
  !***DESCRIPTION
  !
  ! *Usage:
  !
  !      REAL TANH, X, Y
  !      Y = TANH(X)
  !
  ! *Arguments:
  !
  !    X       :IN  This is the argument.  It will not be modified by
  !                 TANH.
  !
  ! *Function Return Values:
  !
  !    TANH    :    the hyperbolic tangent of a complex argument.
  !
  ! *Description:
  !
  !    TANH evaluates the hyperbolic tangent of an argument.
  !
  !    SERIES FOR TANH    ON THE INTERVAL  0.          TO  1.00000D+00
  !                                        WITH WEIGHTED ERROR   9.88E-18
  !                                         LOG WEIGHTED ERROR  17.01
  !                               SIGNIFICANT FIGURES REQUIRED  16.25
  !                                    DECIMAL PLACES REQUIRED  17.62
  !
  !***REFERENCES  (NONE)
  !***ROUTINES CALLED  ALOG, CSEVL, EXP, INITS, R1MACH, SQRT
  !***REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   801001  Revisions by W. Fullerton.
  !   900525  Prologue and code revised to conform to 1990 SLATEC
  !           standards.  (SMR)
  !***END PROLOGUE  TANH
  !     .. Scalar Arguments ..
  REAL X
  !     .. Local Scalars ..
  REAL sqeps, xmax, y
  INTEGER nterms
  LOGICAL first
  !     .. Local Arrays ..
  REAL tanhcs(17)
  !     .. External Functions ..
  REAL ALOG, CSEVL, EXP, R1MACH, SQRT
  INTEGER INITS
  EXTERNAL ALOG, CSEVL, EXP, R1MACH, SQRT, INITS
  !     .. Intrinsic Functions ..
  INTRINSIC ABS, SIGN
  !     .. Save statement ..
  SAVE first, nterms, sqeps, xmax
  !     .. Data statements ..
  DATA tanhcs(1)/ - .25828756643634710E0/
  DATA tanhcs(2)/ - .11836106330053497E0/
  DATA tanhcs(3)/.009869442648006398E0/
  DATA tanhcs(4)/ - .000835798662344582E0/
  DATA tanhcs(5)/.000070904321198943E0/
  DATA tanhcs(6)/ - .000006016424318120E0/
  DATA tanhcs(7)/.000000510524190800E0/
  DATA tanhcs(8)/ - .000000043320729077E0/
  DATA tanhcs(9)/.000000003675999055E0/
  DATA tanhcs(10)/ - .000000000311928496E0/
  DATA tanhcs(11)/.000000000026468828E0/
  DATA tanhcs(12)/ - .000000000002246023E0/
  DATA tanhcs(13)/.000000000000190587E0/
  DATA tanhcs(14)/ - .000000000000016172E0/
  DATA tanhcs(15)/.000000000000001372E0/
  DATA tanhcs(16)/ - .000000000000000116E0/
  DATA tanhcs(17)/.000000000000000009E0/
  DATA first/.TRUE./
  !***FIRST EXECUTABLE STATEMENT  TANH
  IF ( first ) THEN
    nterms = INITS(tanhcs,17,0.1*R1MACH(3))
    sqeps = SQRT(3.0E0*R1MACH(3))
    xmax = -0.5E0*ALOG(R1MACH(3))
  ENDIF
  first = .FALSE.
  !
  y = ABS(X)
  IF ( y<=1.0E0 ) THEN
    TANH = X
    IF ( y>sqeps ) TANH = X*(1.0E0+CSEVL(2.0E0*X*X-1.0E0,tanhcs,nterms))
  ELSEIF ( y<=xmax ) THEN
    y = EXP(y)
    TANH = SIGN((y-1.0E0/y)/(y+1.0E0/y),X)
  ELSE
    TANH = SIGN(1.0E0,X)
  ENDIF
  !
END FUNCTION TANH
