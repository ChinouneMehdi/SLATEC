!** RUNIF
REAL(SP) FUNCTION RUNIF(T,N)
  !>
  !  Generate a uniformly distributed random number.
  !***
  ! **Library:**   SLATEC (FNLIB)
  !***
  ! **Category:**  L6A21
  !***
  ! **Type:**      SINGLE PRECISION (RUNIF-S)
  !***
  ! **Keywords:**  FNLIB, RANDOM NUMBER, SPECIAL FUNCTIONS, UNIFORM
  !***
  ! **Author:**  Fullerton, W., (LANL)
  !***
  ! **Description:**
  !
  ! This random number generator is portable among a wide variety of
  ! computers.  It generates a random number between 0.0 and 1.0 accord-
  ! ing to the algorithm presented by Bays and Durham (TOMS, 2, 59,
  ! 1976).  The motivation for using this scheme, which resembles the
  ! Maclaren-Marsaglia method, is to greatly increase the period of the
  ! random sequence.  If the period of the basic generator (RAND) is P,
  ! then the expected mean period of the sequence generated by RUNIF is
  ! given by   new mean P = SQRT (PI*FACTORIAL(N)/(8*P)),
  ! where FACTORIAL(N) must be much greater than P in this asymptotic
  ! formula.  Generally, N should be around 32 if P=4.E6 as for RAND.
  !
  !             Input Argument --
  ! N      ABS(N) is the number of random numbers in an auxiliary table.
  !        Note though that ABS(N)+1 is the number of items in array T.
  !        If N is positive and differs from its value in the previous
  !        invocation, then the table is initialized for the new value of
  !        N.  If N is negative, ABS(N) is the number of items in an
  !        auxiliary table, but the tables are now assumed already to
  !        be initialized.  This option enables the user to save the
  !        table T at the end of a long computer run and to restart with
  !        the same sequence.  Normally, RUNIF would be called at most
  !        once with negative N.  Subsequent invocations would have N
  !        positive and of the correct magnitude.
  !
  !             Input and Output Argument  --
  ! T      an array of ABS(N)+1 random numbers from a previous invocation
  !        of RUNIF.  Whenever N is positive and differs from the old
  !        N, the table is initialized.  The first ABS(N) numbers are the
  !        table discussed in the reference, and the N+1 -st value is Y.
  !        This array may be saved in order to restart a sequence.
  !
  !             Output Value --
  ! RUNIF  a random number between 0.0 and 1.0.
  !
  !***
  ! **References:**  (NONE)
  !***
  ! **Routines called:**  RAND

  !* REVISION HISTORY  (YYMMDD)
  !   770401  DATE WRITTEN
  !   890531  Changed all specific intrinsics to generic.  (WRB)
  !   890531  REVISION DATE from Version 3.2
  !   891214  Prologue converted to Version 4.0 format.  (BAB)
  !   910819  Added EXTERNAL statement for RAND due to problem on IBM
  !           RS 6000.  (WRB)

  INTEGER :: N
  REAL(SP) :: T(ABS(N)+1)
  INTEGER :: i, j
  REAL(SP) :: dummy
  REAL(SP), SAVE :: floatn
  INTEGER, SAVE :: nold = -1
  !* FIRST EXECUTABLE STATEMENT  RUNIF
  IF ( N/=nold ) THEN
    !
    nold = ABS(N)
    floatn = nold
    IF ( N<0 ) dummy = RAND(T(nold+1))
    IF ( N>=0 ) THEN
      !
      DO i = 1, nold
        T(i) = RAND(0.)
      END DO
      T(nold+1) = RAND(0.)
    END IF
  END IF
  !
  j = INT( T(nold+1)*floatn ) + 1
  T(nold+1) = T(j)
  RUNIF = T(j)
  T(j) = RAND(0.)
  !
END FUNCTION RUNIF
